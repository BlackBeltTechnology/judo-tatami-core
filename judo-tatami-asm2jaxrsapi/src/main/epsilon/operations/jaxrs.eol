@cached
operation String normalizeName() : String {
	return self.replace("__", "\\$");
}

@cached
operation String toJAXRSPath() : String {
    return self.replace("\\.__UnboundServices", "").replace("[__#\\.]", "/").replace("[^0-9a-zA-Z_/]", "/").replace("\\$", "/");
}

/*
 DTO
 */

operation genDto(dto : ASM!EClass) {
	var t = TemplateFactory.load("Dto.java.egl");
	t.populate("dto", dto);
	t.generate(dto.getDtoJavaFileName());
	t.reset();
}

@cached
operation ASM!EClass getPackagePrefix() : String {
    return "rest.";
}

@cached
operation ASM!EClass getDtoPackageName() : String {
	return self.getPackagePrefix() + asmUtils.getPackageFQName(self.ePackage);
}

@cached
operation ASM!EClass getDtoTypeName() : String {
	return self.getDtoPackageName() + "." + self.name.normalizeName();
}

@cached
operation ASM!EClass getDtoJavaFileName() : String {
	return self.getDtoPackageName().replaceAll("\\.", "/") + "/" + self.name.normalizeName() + ".java";
}

@cached
operation ASM!EAttribute getTypeDefinition() : String {

	if (self.upperBound == -1) {
		return "List<" + self.eType.instanceClass.name + ">";
	} else if (self.eType.instanceClass.isDefined()) {
		return self.eType.instanceClass.name;
	} else {
		return "Integer";
	}
}

@cached
operation ASM!EReference getTypeDefinition() : String {
	if (self.upperBound == -1) {
		return "List<" + self.getBareTypeDefinition() + ">";
	} else {
		return self.getBareTypeDefinition();
	}
}

@cached
operation ASM!EReference getBareTypeDefinition() : String {
	return self.eReferenceType.getDtoPackageName() + "." + self.eReferenceType.name;
}

@cached
operation ASM!EClass getApplicationPackageName() : String {
	return self.getPackagePrefix() + asmUtils.getClassifierFQName(self);
}

@cached
operation ASM!EClass getApplicationConfigJavaFileName() : String {
	return self.getApplicationPackageName().replaceAll("\\.", "/") + "/ApplicationConfig.java";
}

@cached
operation ASM!EClass getApplicationConfigScrXmlFileName() : String {
	return "OSGI-INF/" + self.getApplicationPackageName() + ".ApplicationConfig.xml";
}

/*
 Application Config
 */
operation genApplication(ap : ASM!EClass, serviceInstances : Map) {
	var t = TemplateFactory.load("ApplicationConfig.java.egl");
	t.populate("ap", ap);
	t.populate("className", "ApplicationConfig");
	t.populate("packagePath", ap.getApplicationPackageName());
	t.populate("restPath", "/" + asmUtils.getClassifierFQName(ap).toJAXRSPath());
	t.populate("serviceInstances", serviceInstances);
	t.generate(ap.getApplicationConfigJavaFileName());
	t.reset();

	var t = TemplateFactory.load("ApplicationConfig.scr.xml.egl");
	t.populate("className", "ApplicationConfig");
	t.populate("packagePath", ap.getApplicationPackageName());
	t.populate("serviceInstances", serviceInstances);
	t.generate(ap.getApplicationConfigScrXmlFileName());
	t.reset();
}

/*
 Unbound Service
 */
operation genUnboundService(ap : ASM!EClass, operations : Collection) {
	var t = TemplateFactory.load("RestService.java.egl");
	t.populate("boundService", false);
	t.populate("className", ap.getUnboundServiceClassName());
	t.populate("packagePath", ap.getUnboundServicePackageName());
	t.populate("operations", operations);
	t.populate("restPath", "/");
	t.populate("fullyQualifiedOperation", true);

	t.generate(ap.getUnboundServiceJavaFileName());
	t.reset();

	var t = TemplateFactory.load("RestService.scr.xml.egl");
	t.populate("className", ap.getUnboundServiceClassName());
	t.populate("packagePath", ap.getUnboundServicePackageName());
	t.generate(ap.getUnboundServiceScrXmlFileName());
	t.reset();
}

operation ASM!EClass getUnboundServicePackageName() : String {
	return self.getPackagePrefix() + asmUtils.getClassifierFQName(self);
}

operation ASM!EClass getUnboundServiceClassName() : String {
	return "UnboundServices";
}

operation ASM!EClass getUnboundServiceJavaFileName() : String {
	return self.getUnboundServicePackageName().replaceAll("\\.", "/") + "/" + self.getUnboundServiceClassName() + ".java";
}

operation ASM!EClass getUnboundServiceScrXmlFileName() : String {
	return "OSGI-INF/" + self.getUnboundServicePackageName() + "." + self.getUnboundServiceClassName() + ".xml";
}

/*
 Exposed mapped transfer object
 */
operation genExposedMappedTransferObject(class : ASM!EClass, ap : ASM!EClass, operations: Collection) {
	var t = TemplateFactory.load("RestService.java.egl");
	t.populate("boundService", true);
	t.populate("className", class.name.normalizeName());
	t.populate("packagePath", class.getExposedMappedTransferObjectPackageName(ap));
	t.populate("operations", operations);
	t.populate("restPath", "/" + asmUtils.getPackageFQName(class.ePackage).toJAXRSPath() + "/" + class.name.normalizeName().toJAXRSPath());
	t.populate("fullyQualifiedOperation", false);

	t.generate(class.getExposedMappedTransferObjectJavaFileName(ap));
	t.reset();

	var t = TemplateFactory.load("RestService.scr.xml.egl");
	t.populate("className", class.name.normalizeName());
	t.populate("packagePath", class.getExposedMappedTransferObjectPackageName(ap));
	t.generate(class.getExposedMappedTransferObjectScrXmlFileName(ap));
	t.reset();
}

operation ASM!EClass getExposedMappedTransferObjectPackageName(ap: ASM!EClass) : String {
	return self.getPackagePrefix() + asmUtils.getClassifierFQName(ap) + "." + asmUtils.getPackageFQName(self.ePackage);
}

operation ASM!EClass getExposedMappedTransferObjectScrXmlFileName(ap: ASM!EClass) : String {
	return "OSGI-INF/" + self.getExposedMappedTransferObjectPackageName(ap) + "." + self.name.normalizeName() + ".xml";
}

operation ASM!EClass getExposedMappedTransferObjectJavaFileName(ap: ASM!EClass) : String {
	return self.getExposedMappedTransferObjectPackageName(ap)
		.replaceAll("\\.", "/") + "/" + self.name.normalizeName() + ".java";
}