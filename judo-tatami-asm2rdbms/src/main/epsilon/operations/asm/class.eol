import "modelElement.eol";
import "package.eol";

@cached
operation ASM!EClass hasSupertype() : Boolean {
	return self.eSuperTypes.size > 0;
}

@cached
operation ASM!EClass isEntity() : Boolean {
	return self.annotatedAsTrue("entity");
}

@cached
operation ASM!EClass isFacade() : Boolean {
	return self.annotatedAsTrue("facade");
}

@cached
operation ASM!EClassifier getPackage() : ASM!EPackage {
	return ASM!EPackage.all.selectOne(p | p.eClassifiers.contains(self));
}

@cached
operation ASM!EClassifier getFQName() : String {
	return self.ePackage.getFullName() + "." + self.name;
}

@cached
operation ASM!EClass getNestedClasses() : Collection {
    return ASM!EClass.all.select(c | c.name.startsWith(self.name + "ʘ") and not "ʘ".isSubstringOf(c.name.substring(self.name.length() + 1)));
}

@cached
operation ASM!EClass getContainerClass() : ASM!EClass {
    return ASM!EClass.all.selectOne(c | self.name.startsWith(c.name + "ʘ") and not "ʘ".isSubstringOf(self.name.substring(c.name.length() + 1)));
}
