/* Every entity table have an ID */
rule EClassToTableIdField
	transform s : ASM!EClass
	to t : RDBMS!RdbmsIdentifierField {
		guard : asmUtils.isEntityType(s)
		t.name = asmUtils.getClassifierFQName(s) + "#_id";
	    t.uuid = asmUtils.getClassifierFQName(s) + "#_id";
        log.debug("    Add primary key: " + t.name);
        var table = s.equivalent("EClassToRdbmsTable");
	    table.fields.add(t);
		table.primaryKey = t;
		t.sqlName = "ID";
		t.fillType("java.util.UUID");
}

/* Every entity table have a type */
rule EClassToTableTypeField
	transform s : ASM!EClass
	to t : RDBMS!RdbmsValueField {
		guard : asmUtils.isEntityType(s)
		t.name = asmUtils.getClassifierFQName(s) + "#_type";
	    t.uuid = asmUtils.getClassifierFQName(s) + "#_type";
        log.debug("    Add type: " + t.name);
        var table = s.equivalent("EClassToRdbmsTable");
	    table.fields.add(t);
		t.sqlName = "TYPE";
		t.mandatory = true;
		t.fillType("java.lang.String");
}

@primary
rule EClassToRdbmsTable
	transform s : ASM!EClass
	to t : RDBMS!RdbmsTable {
		guard : asmUtils.isEntityType(s)
        log.debug(" Add table: " + asmUtils.getClassifierFQName(s));
		s.root().equivalent("rootPackegeToModel").rdbmsTables.add(t);
	    t.sqlName = s.tableSqlName();
	    t.name = asmUtils.getClassifierFQName(s);
	    t.uuid = asmUtils.getClassifierFQName(s);
		for (sup in s.eSuperTypes.select(sup | asmUtils.isEntityType(sup))) {
			t.parents.add(sup.equivalent("EClassToRdbmsTable"));
		}
}
