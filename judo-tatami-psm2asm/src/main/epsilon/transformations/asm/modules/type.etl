import "../utils/_importUtils.eol";

@greedy
rule CreateEnumeration
	transform s : JUDOPSM!EnumerationType
	to t : ASM!EEnum {
		t.name = s.name;
		s.eContainer.asmEquivalent().eClassifiers.add(t);
		for (m in s.members) {
			var l = new ASM!EEnumLiteral;
			l.value = m.ordinal;
			l.literal = m.name;
			l.name = m.name;
			t.eLiterals.add(l);
		}
	}

@greedy
rule CreateStringType
    transform s : JUDOPSM!StringType
    to t : ASM!EDataType {
        t.name = s.name;
        t.instanceClassName = "java.lang.String";
        
        s.eContainer.asmEquivalent().eClassifiers.add(t);
    }

@greedy
rule CreateIntegerType
    transform s : JUDOPSM!NumericType
    to t : ASM!EDataType {
        guard: s.isInteger()
        
        t.name = s.name;
        
        if (s.precision <= 9 and s.precision > 0) {
            t.instanceClassName = "java.lang.Integer";
        } else if (s.precision <= 19 and s.precision > 9) {
            t.instanceClassName = "java.lang.Long";
        } else {
            t.instanceClassName = "java.math.BigDecimal";
        }
        
        s.eContainer.asmEquivalent().eClassifiers.add(t);
    }

@greedy
rule CreateDecimalType
    transform s : JUDOPSM!NumericType
    to t : ASM!EDataType {
        guard: s.isDecimal()
        
        t.name = s.name;
        if (s.precision <= 7 and s.precision > 0 and s.scale <= 4) {
            t.instanceClassName = "java.lang.Float";
        } else if (s.precision <= 15 and s.precision > 7 and s.scale <= 4) {
            t.instanceClassName = "java.lang.Double";
        } else {
            t.instanceClassName = "java.math.BigDecimal";
        }
        
        s.eContainer.asmEquivalent().eClassifiers.add(t);
    }

rule CreateMeasuredAnnotationOfIntegerType
    transform s : JUDOPSM!MeasuredType
    to t : ASM!EAnnotation {
        t.source = asmUtils.getAnnotationUri("measured");

        var unit = new ASM!EStringToStringMapEntry;
        unit.key = "unit";
        unit.value = s.storeUnit.name;
        t.details.add(unit);

        var measure = new ASM!EStringToStringMapEntry;
        measure.key = "measure";
        measure.value = psmUtils.namespaceElementToString(s.storeUnit.eContainer).replace("::", ".");
        t.details.add(measure);

        if (s.isInteger()) {
            s.equivalent("CreateIntegerType").eAnnotations.add(t);
        } else if (s.isDecimal()) {
            s.equivalent("CreateDecimalType").eAnnotations.add(t);
        }
    }

rule CreateRegexpAnnotationOfStringType
    transform s : JUDOPSM!StringType
    to t : ASM!EAnnotation {
        guard: s.regExp.isDefined() and s.regExp.trim().length() > 0
        t.source = asmUtils.getAnnotationUri("pattern");

        var regExp = new ASM!EStringToStringMapEntry;
        regExp.key = "value";
        regExp.value = s.regExp;
        t.details.add(regExp);

        s.equivalent("CreateStringType").eAnnotations.add(t);
    }

@greedy
rule CreateBooleanType
    transform s : JUDOPSM!BooleanType
    to t : ASM!EDataType {
        t.name = s.name;
        t.instanceClassName = "java.lang.Boolean";
        
        s.eContainer.asmEquivalent().eClassifiers.add(t);
    }

@greedy
rule CreatePasswordType
    transform s : JUDOPSM!PasswordType
    to t : ASM!EDataType {
        throw "Password type is not supported yet.";
    }

@greedy
rule CreateXMLType
    transform s : JUDOPSM!XMLType
    to t : ASM!EDataType {
        throw "XML type is not supported yet.";
    }

@greedy
rule CreateDateType
    transform s : JUDOPSM!DateType
    to t : ASM!EDataType {
        t.name = s.name;
        t.instanceClassName = "java.time.LocalDate";
        
        s.eContainer.asmEquivalent().eClassifiers.add(t);
    }

@greedy
rule CreateTimestampType
    transform s : JUDOPSM!TimestampType
    to t : ASM!EDataType {
        t.name = s.name;
        t.instanceClassName = "java.time.OffsetDateTime";
        
        s.eContainer.asmEquivalent().eClassifiers.add(t);
    }

@greedy
rule CreateCustomType
    transform s : JUDOPSM!CustomType
    to t : ASM!EDataType {
        guard: not s.isNumeric() and not s.isBoolean() and not s.isEnumeration() and not s.isString() and not s.isDate() and not s.isTimestamp() and not s.isKindOf(JUDOPSM!PasswordType) and not s.isKindOf(JUDOPSM!XMLType)
        
        t.name = s.name;
        t.instanceClassName = "java.lang.Object";
        
        s.eContainer.asmEquivalent().eClassifiers.add(t);
    }
