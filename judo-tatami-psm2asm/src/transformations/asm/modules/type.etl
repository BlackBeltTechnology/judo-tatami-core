import "../../../operations/psm/type/_importType.eol";
import "../../../operations/asm/_importAsm.eol";
import "../utils/_importUtils.eol";

rule CreateEnumeration
	transform s : JUDOPSM!Enumeration
	to t : ASM!EEnum {
		t.name = s.name;
		s.getPackage().equivalent().eClassifiers.add(t);
		for (m in s.members) {
			var l = new ASM!EEnumLiteral;
			l.value = m.ordinal;
			l.literal = m.name;
			l.name = m.name;
			t.eLiterals.add(l);
		}
	}

rule CreateStructureClass
	transform s : JUDOPSM!Structure
	to t : ASM!EClass {
		t.name = s.name;
		s.getPackage().equivalent().eClassifiers.add(t);
		for (f in s.fields) {
			var type = f.type.getEcoreType();

			var a = new ASM!EAttribute;
			a.name = f.name;

			if (f.required) {
				a.lowerBound = 1;
			} else {
				a.lowerBound = 0;
			}

			if (type.isTypeOf(ASM!EDataType)) {
				a.eType = f.type.getEcoreType();
			} else if (f.type.isKindOf(JUDOPSM!Reference)) {
				f.type.name.println("Reference in structure: ");
				a.eType = "JUUID".getJudoDataType();
			}

			var annotations = f.type.createASMAnnotations();
			if (not annotations.isEmpty()) {
				var annotation = new ASM!EAnnotation;
				annotation.source = extendedMetadataURI;
				annotation.details.addAll(annotations);
				a.eAnnotations.add(annotation);
			}

			t.eStructuralFeatures.add(a);
		}
	}
