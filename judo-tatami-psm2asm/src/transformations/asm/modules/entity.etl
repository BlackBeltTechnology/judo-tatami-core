import "../../../operations/psm/data/_importData.eol";
import "../utils/_importUtils.eol";

rule CreateEntityClass
	transform s : JUDOPSM!EntityType
	to t : ASM!EClass {
		t.name = s.name;
		t.abstract = s.abstract;
		for (super in s.superEntityTypes) {
			t.eSuperTypes.add(super.equivalent());
		}
		s.getPackage().equivalent().eClassifiers.add(t);
		
		if (s.superEntityTypes.isEmpty()) {
			var id = new ASM!EAttribute;
			id.name = "id";
			id.iD = true;
			id.changeable = false;
			id.eType = "JUUID".getJudoDataType();
			t.eStructuralFeatures.add(id);

			var annotation = new ASM!EAnnotation;
			annotation.source = extendedMetadataURI;
			var aEntity = new ASM!EStringToStringMapEntry;
			aEntity.key = "id";
			aEntity.value = "true";
			annotation.details.add(aEntity);
			id.eAnnotations.add(annotation);
		}

		var annotation = new ASM!EAnnotation;
		annotation.source = extendedMetadataURI;
		var aEntity = new ASM!EStringToStringMapEntry;
		aEntity.key = "entity";
		aEntity.value = "true";
		annotation.details.add(aEntity);
		t.eAnnotations.add(annotation);
	}

rule CreateAttribute
	transform s : JUDOPSM!Attribute
	to t : ASM!EAttribute {
		t.name = s.name;
		if (s.required) {
			t.lowerBound = 1;
		} else {
			t.lowerBound = 0;
		}

		if (s.identifier) {
			t.iD = true;
		}

		t.eType = s.primitive.getEcoreType();

		var annotations = s.primitive.createASMAnnotations();
		if (not annotations.isEmpty()) {
			var annotation = new ASM!EAnnotation;
			annotation.source = extendedMetadataURI;
			annotation.details.addAll(annotations);
			t.eAnnotations.add(annotation);
		}

		s.getEntityType().equivalent().eStructuralFeatures.add(t);
	}

rule CreateReference
	transform s : JUDOPSM!Reference
	to t : ASM!EReference {
		t.name = s.name;

		t.lowerBound = s.lower;
		t.upperBound = s.upper;

		var annotations = s.reference.createASMAnnotations();
		if (not annotations.isEmpty()) {
			var annotation = new ASM!EAnnotation;
			annotation.source = extendedMetadataURI;
			annotation.details.addAll(annotations);
			t.eAnnotations.add(annotation);
		}

		s.getEntityType().equivalent().eStructuralFeatures.add(t);	
	
		t.eType = s.target.name.getEntityTypeReference();
		t.eOpposite = s.partner.equivalent();

		t.containment = s.composition;
	}
