import "../utils/changeset.eol";

/////////////////////////////////////////
// BACKUP TABLES

@abstract
rule BackupTables
    transform s : RDBMS!RdbmsTableOperation
    to t : LIQUIBASE!CreateTable {
        var useTable;
        if (s.isTypeOf(RDBMS!RdbmsModifyTableOperation)) useTable = s.previousTable;
        else useTable = s.table;

        t.tableName = useTable.sqlName;
        t.remarks = useTable.uuid;

        for(field in useTable.fields) {
            var column : new LIQUIBASE!Column;
            var constraint : new LIQUIBASE!Constraints;
            column.name = field.sqlName;
            column.remarks = field.uuid;
            column.type = field.toFieldDefinition();
            if (field == field.eContainer.primaryKey or field.mandatory) {
                constraint.primaryKey = field == field.eContainer.primaryKey;
                constraint.nullable = not field.mandatory;
                column.constraints = constraint;
            }
            t.column.add(column);
        }

        targetModel.createChangeSetIfNotExists("backup-table-" + useTable.sqlName, "backup-table", dialect, context);
        targetModel.getChangeSet("backup-table-" + useTable.sqlName).createTable.add(t);
}

rule BackupDeletedTables
    transform s : RDBMS!RdbmsDeleteTableOperation
    to t : LIQUIBASE!CreateTable
    extends BackupTables {
        log.info("(Backup - Delete) CreateTable " + t.tableName + " created");
}

rule BackupModifiedTables
    transform s : RDBMS!RdbmsModifyTableOperation
    to t : LIQUIBASE!CreateTable
    extends BackupTables {
        log.info("(Backup - Modify) CreateTable " + t.tableName + " created");
}

// BACKUP TABLES
/////////////////////////////////////////
// CREATE TABLES

@lazy
rule CreateTableOperationToChangeSet
    transform s : RDBMS!RdbmsCreateTableOperation
    to t : LIQUIBASE!ChangeSet {
        t.id = "create-table-" + s.table.sqlName;
        t.author = "tatami-rdbms2liquibase";
        t.dbms = dialect;
        t.context = context;
        t.logicalFilePath = "create-tables";
        targetModel.changeSet.add(t);
        log.info("ChangeSet " + t.id + " created");
}

rule CreateTableOperationToCreateTable
    transform s : RDBMS!RdbmsCreateTableOperation
    to t : LIQUIBASE!CreateTable {
        t.tableName = s.table.sqlName;
        t.remarks = s.table.uuid;

        for(field in s.table.fields) {
            var column : new LIQUIBASE!Column;
            var constraint : new LIQUIBASE!Constraints;
            column.name = field.sqlName;
            column.remarks = field.uuid;
            column.type = field.toFieldDefinition();
            if (field == field.eContainer.primaryKey or field.mandatory) {
                constraint.primaryKey = field == field.eContainer.primaryKey;
                constraint.nullable = not field.mandatory;
                column.constraints = constraint;
            }
            t.column.add(column);
        }

        s.equivalent("CreateTableOperationToChangeSet").createTable.add(t);
        log.info("CreateTable " + t.tableName + " created");
}

// CREATE TABLES
/////////////////////////////////////////
// RENAME TABLES

rule ModifyTableOperationToRanameTable
    transform s : RDBMS!RdbmsModifyTableOperation
    to t : LIQUIBASE!RenameTable {
        guard: s.nameChanged
        t.oldTableName = s.previousTable.sqlName;
        t.newTableName = s.table.sqlName;

        targetModel.createChangeSetIfNotExists("rename-tables", "rename-tables", dialect, context);
        targetModel.getChangeSet("rename-tables").renameTable.add(t);
        log.info("RenameTable (" + t.oldTableName + " -> " + t.newTableName + ") added to " + targetModel.getChangeSet("rename-tables").id);
}

// RENAME TABLES
/////////////////////////////////////////
// RENAME FIELDS

rule RenameToRenameColumn
    transform s : RDBMS!RdbmsModifyFieldOperation
    to t : LIQUIBASE!RenameColumn {
        guard: s.nameChanged
        t.newColumnName = s.field.sqlName;
        t.oldColumnName = s.previousField.sqlName;
        t.tableName = s.eContainer.table.sqlName;
        t.remarks = s.field.uuid;

        targetModel.createChangeSetIfNotExists("rename-fields-for-" + s.eContainer.table.sqlName, "rename-fields", dialect, context);
        targetModel.getChangeSet("rename-fields-for-" + s.eContainer.table.sqlName).renameColumn.add(t);
        log.info("Rename column added in table " + t.tableName + ": " + t.oldColumnName + " -> " + t.newColumnName);
}

// RENAME FIELDS
/////////////////////////////////////////
// DROP FIELDS

rule DeleteFieldOperationToDropColumn
    transform s : RDBMS!RdbmsDeleteFieldOperation
    to t : LIQUIBASE!DropColumn {
        t.tableName = s.eContainer.table.sqlName;
        t.columnName = s.field.sqlName;

        targetModel.createChangeSetIfNotExists("drop-fields-from-" + s.eContainer.table.sqlName, "drop-fields", dialect, context);
        targetModel.getChangeSet("drop-fields-from-" + s.eContainer.table.sqlName).dropColumn.add(t);
        log.info("Drop field added: " + t.columnName + "(" + t.tableName + ")");
}

// DROP FIELDS
/////////////////////////////////////////
// CREATE FIELDS

rule CreateFieldOperationToAddColumnDefOnExistingTable
    transform s : RDBMS!RdbmsCreateFieldOperation
    to t : LIQUIBASE!AddColumnDef {
        t.name = s.field.sqlName;
        t.type = s.field.toFieldDefinition();
        t.remarks = s.field.uuid;
        if (s.field == s.field.eContainer.primaryKey) {
            t.constraints = new LIQUIBASE!Constraints(primaryKey = true, nullable = not s.field.mandatory);
        }

        targetModel.createChangeSetIfNotExists("create-fields-for-" + s.eContainer.table.sqlName, "create-fields", dialect, context);
        var changeSet = targetModel.getChangeSet("create-fields-for-" + s.eContainer.table.sqlName);
        if (changeSet.addColumn.size == 0) {
            changeSet.addColumn.add(new LIQUIBASE!AddColumn(tableName = s.eContainer.table.sqlName));
            log.info("AddColumn created in " + changeSet.id);
        }
        changeSet.addColumn[0].column.add(t);
        log.info("AddColumnDef created in " + changeSet.id + ": " + t.name);
}

// CREATE FIELDS
/////////////////////////////////////////
// MODIFY TYPE/SIZE

rule RdbmsModifyFieldOperationToModifyDataType
    transform s : RDBMS!RdbmsModifyFieldOperation
    to t : LIQUIBASE!ModifyDataType {
        guard: s.typeChanged or s.sizeChanged
        t.tableName = s.eContainer.table.sqlName;
        t.columnName = s.field.sqlName;
        t.newDataType = s.field.toFieldDefinition();

        targetModel.createChangeSetIfNotExists("modify-data-types-in-" + s.eContainer.table.sqlName, "modify-data-types", dialect, context);
        targetModel.getChangeSet("modify-data-types-in-" + s.eContainer.table.sqlName).modifyDataType.add(t);
        log.info("ModifyDataType added for " + t.columnName + "(" + t.tableName + "): " + s.previousField.toFieldDefinition() + " -> " + s.field.toFieldDefinition());
}

// MODIFY TYPE/SIZE
/////////////////////////////////////////
// DROP TABLES

rule DeleteTableOperationToDropTable
    transform s : RDBMS!RdbmsDeleteTableOperation
    to t : LIQUIBASE!DropTable {
        t.tableName = s.table.sqlName;

        targetModel.createChangeSetIfNotExists("drop-tables", "drop-tables", dialect, context);
        targetModel.getChangeSet("drop-tables").dropTable.add(t);
        log.info("Drop table added: " + t.tableName);
}

// DROP TABLES
/////////////////////////////////////////
