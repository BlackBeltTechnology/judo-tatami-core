import "liquibase/utils/changeset.eol";

/////////////////////////////////////////
// INDEX

rule DropAllIndexes
    transform s : RDBMS!RdbmsIndex
    to t : BEFORE_INCREMENTAL!DropIndex {
        guard: previousModel.contains(s.eContainer)
        t.tableName = s.eContainer.sqlName;
        t.indexName = s.sqlName;

        beforeIncrementalModel.createChangeSetIfNotExists("drop-indexes-from-" + s.eContainer.sqlName, "drop-indexes", dialect, context);
        beforeIncrementalModel.getChangeSet("drop-indexes-from-" + s.eContainer.sqlName).dropIndex.add(t);
        log.info("DropIndex added: " + t.indexName + "(" + t.tableName + ")");
}

// INDEX
/////////////////////////////////////////
// UNIQUE CONSTRAINT

rule DropAllUniqueConstraints
    transform s : RDBMS!RdbmsUniqueConstraint
    to t : BEFORE_INCREMENTAL!DropUniqueConstraint {
        guard: previousModel.contains(s.eContainer)
        t.tableName = s.eContainer.sqlName;
        t.uniqueColumns = s.fields[0].sqlName;
        if (s.fields.size() > 1) {
            var i = 1;
            while (i < s.fields.size()) {
                t.uniqueColumns += "," + s.fields[i].sqlName;
                i++;
            }
        }

        beforeIncrementalModel.createChangeSetIfNotExists("drop-unique-constraint-from-" + s.eContainer.sqlName, "drop-unique-constraint", dialect, context);
        beforeIncrementalModel.getChangeSet("drop-unique-constraint-from-" + s.eContainer.sqlName).dropUniqueConstraint.add(t);
        log.info("DropUniqueConstraint added: " + t.uniqueColumns + "(" + t.tableName + ")");
}

// UNIQUE CONSTRAINT
/////////////////////////////////////////
// NOT NULL CONSTRAINT

@abstract
rule DropAllNotNullConstraints
    transform s : RDBMS!RdbmsField
    to t : BEFORE_INCREMENTAL!DropNotNullConstraint {
        guard: previousModel.contains(s.eContainer) and s.mandatory
        t.tableName = s.eContainer.sqlName;
        t.columnName = s.sqlName;

        beforeIncrementalModel.createChangeSetIfNotExists("drop-not-null-constraints-from-" + s.eContainer.sqlName, "drop-not-null-constraints", dialect, context);
        beforeIncrementalModel.getChangeSet("drop-not-null-constraints-from-" + s.eContainer.sqlName).dropNotNullConstraint.add(t);
        log.info("DropNotNullConstraint added to " + t.columnName + "(" + t.tableName + ")");
}

rule DropAllNotNullConstraintsFromValueField
    transform s : RDBMS!RdbmsValueField
    to t : BEFORE_INCREMENTAL!DropNotNullConstraint
    extends DropAllNotNullConstraints {
}

rule DropAllNotNullConstraintsFromIdentifierField
    transform s : RDBMS!RdbmsIdentifierField
    to t : BEFORE_INCREMENTAL!DropNotNullConstraint
    extends DropAllNotNullConstraints {
}

rule DropAllNotNullConstraintsFromForeignKey
    transform s : RDBMS!RdbmsForeignKey
    to t : BEFORE_INCREMENTAL!DropNotNullConstraint
    extends DropAllNotNullConstraints {
}


// NOT NULL CONSTRAINT
/////////////////////////////////////////
// FOREIGN KEY

rule DropAllForeignKeys
    transform s : RDBMS!RdbmsForeignKey
    to t : BEFORE_INCREMENTAL!DropForeignKeyConstraint {
        guard: previousModel.contains(s.eContainer)
        t.baseTableName = s.eContainer.sqlName;
        t.constraintName = s.foreignKeySqlName;

        beforeIncrementalModel.createChangeSetIfNotExists("drop-foreign-keys-from-" + s.eContainer.sqlName, "drop-foreign-keys", dialect, context);
        beforeIncrementalModel.getChangeSet("drop-foreign-keys-from-" + s.eContainer.sqlName).dropForeignKeyConstraint.add(t);
        log.info("DropForeignKeyConstraint added: " + t.constraintName + "(" + t.baseTableName + ")");
}

// FOREIGN KEY
/////////////////////////////////////////
