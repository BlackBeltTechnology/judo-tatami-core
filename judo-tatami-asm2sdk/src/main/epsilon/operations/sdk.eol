@cached
operation String normalizeName() : String {
	return self.replace("[__]", "\\$");
}

@cached
operation ASM!EOperation getFullOperationPath() : String {
	return (asmUtils.getPackageFQName(self.eContainingClass.ePackage) + "." + self.name).replaceAll("\\.", "/");
}

@cached
operation ASM!EAttribute getTypeDefinition() : String {	
	if (self.upperBound == -1) {
		return "List<" + self.eType.instanceClass.name + ">";
	} else {
		return self.eType.instanceClass.name;
	}
}

@cached
operation ASM!EReference getBareTypeDefinition() : String {	
	return self.eReferenceType.getDtoPackageName() + "." + self.eReferenceType.name;
}


@cached
operation ASM!EReference getTypeDefinition() : String {	
	if (self.upperBound == -1) {
		return "List<" + self.getBareTypeDefinition() + ">";
	} else {
		return self.getBareTypeDefinition();
	}
}

@cached
operation ASM!EClass collectAllReferenceType() : Set {	
	var ret : new Set();
	for (r in self.eAllReferences) {
		ret.add(r.eReferenceType);
	}
	return ret;
}

@cached
operation Collection collectAllReferenceType() : Set {	
	var ret : new Set();	
	for (c : ASM!EClass in self) {
		ret.add(c);
		ret.addAll(c.collectAllReferenceType());
	}
	return ret;
}

@cached
operation Collection collectAllReferenceTypeRecursively() : Set {	
	var ret : new Set();	
	ret.addAll(self);

	var sizeDiffers = true;
	while (sizeDiffers) {
	    var curSize = ret.size();
		ret = ret.collectAllReferenceType();
		sizeDiffers = (curSize <> ret.size());
	}
	return ret;
}

/*
 DTO
 */
@cached
operation ASM!EClass idType() : String {
	return "java.util.UUID";
}

@cached
operation ASM!EClass getDtoPackageName() : String {
	return "sdk." + asmUtils.getPackageFQName(self.ePackage);
}

@cached
operation ASM!EClass getDtoTypeName() : String {
	return self.getDtoPackageName() + "." + self.name;
}

@cached
operation ASM!EClass getDtoJavaFileName() : String {
	return self.getDtoPackageName().replaceAll("\\.", "/") + "/" + self.getClassPackage().toLowerCase().replaceAll("\\.", "/") + "/" + self.getClassName() + ".java";
}

@cached
operation ASM!EClass getDtoInterfaceJavaFileName() : String {
	return self.getDtoPackageName().replaceAll("\\.", "/") + "/" + self.getClassPackage().toLowerCase().replaceAll("\\.", "/") + "/" + self.getClassName() + "Interface" + ".java";
}

@cached
operation ASM!EClass getDtoFactoryJavaFileName() : String {
	return self.getDtoPackageName().replaceAll("\\.", "/") + "/" + self.getClassPackage().toLowerCase().replaceAll("\\.", "/") + "/" + self.getClassName() + "Factory" + ".java";
}

operation ASM!EClass getClassName() : String {
    return self.name.split("__")[self.name.split("__").size() - 1];
}

operation ASM!EClass getClassPackage() : String {
    var str : String;
    for(s : String in self.name.split("__")) {
       str += "." + s;
    }
    return str.replace("." + self.getClassName(),"");
}

@cached
operation getAllDTOs(): Collection {
	return ASM!EClass.all.select(class | asmUtils.getExtensionAnnotationListByName(class, "exposedBy").notEmpty() and (not asmUtils.isEntityType(class) or asmUtils.isMappedTransferObjectType(class)));
}

operation genDtoInterface(dto : ASM!EClass) {
	var template = TemplateFactory.load("DtoInterface.java.egl");
	template.populate("dto", dto);
	template.generate(dto.getDtoJavaFileName());
	template.reset();
}

operation genDtoFactory(dto : ASM!EClass) {
	var template = TemplateFactory.load("DtoFactory.java.egl");
	template.populate("dto", dto);
	template.generate(dto.getDtoFactoryJavaFileName());
	template.reset();
}

operation genUnboundServiceInterface(ap : ASM!EClass, operations : Collection) {
	var t = TemplateFactory.load("Service.java.egl");
	
	for(op in operations) {
	   t.populate("packagePath", ap.getUnboundServicePackageName());
	   t.populate("operation", op.name);
	   t.generate(ap.getUnboundServiceJavaFileName(op.name));
	   t.reset();
	}

	/*var t = TemplateFactory.load("Service.scr.xml.egl");
	t.populate("className", ap.getUnboundServiceClassName());
	t.populate("packagePath", ap.getUnboundServicePackageName());
	t.populate("operations", operations);
	t.generate(ap.getUnboundServiceScrXmlFileName());
	t.reset();*/
}

operation genExposedMappedTransferObjectInterface(biog : ASM!EClass, ap : ASM!EClass) {
	var t = TemplateFactory.load("Service.java.egl");

	for(op in asmUtils.getAllOperationsOfBuiltInOperationGroupExposedByAccessPoint(biog, ap)) {
	   t.populate("packagePath", biog.getExposedMappedTransferObjectPackageName(ap));
	   t.populate("operation", op.name);
	   t.generate(biog.getExposedMappedTransferObjectJavaFileName(ap,op.name));
	   t.reset();
	}

	/*var t = TemplateFactory.load("Service.scr.xml.egl");
	t.populate("className", biog.getExposedMappedTransferObjectClassName());
	t.populate("packagePath", biog.getExposedMappedTransferObjectPackageName(ap));
	t.populate("operations", asmUtils.getAllOperationsOfBuiltInOperationGroupExposedByAccessPoint(biog, ap));
	t.generate(biog.getExposedMappedTransferObjectScrXmlFileName(ap));
	t.reset();*/
}

operation ASM!EClass getUnboundServicePackageName() : String {
	return "sdk." + asmUtils.getClassifierFQName(self) + "." + self.getUnboundServiceClassName().toLowerCase();
}

operation ASM!EClass getUnboundServiceClassName() : String {
	return "UnboundServices";
}

operation ASM!EClass getUnboundServiceJavaFileName(opname: String) : String {
	return self.getUnboundServicePackageName().replaceAll("\\.", "/") + "/" + opname.firstToUpperCase() + ".java";
}

operation ASM!EClass getUnboundServiceScrXmlFileName() : String {
	return "OSGI-INF/" + self.getUnboundServicePackageName() + "." + self.getUnboundServiceClassName() + ".xml";
}

operation ASM!EClass getExposedMappedTransferObjectPackageName(ap: ASM!EClass) : String {
	return "sdk." + asmUtils.getClassifierFQName(ap) + "." + asmUtils.getPackageFQName(self.ePackage) + "." + self.getExposedMappedTransferObjectClassName().toLowerCase();
}

@cached
operation ASM!EClass getExposedMappedTransferObjectClassName() : String {

	return self.name.replace("__","\\.");
}

operation ASM!EClass getExposedMappedTransferObjectJavaFileName(ap: ASM!EClass, opname: String) : String {
	return self.getExposedMappedTransferObjectPackageName(ap)
		.replaceAll("\\.", "/") + "/" + opname.firstToUpperCase() + ".java";
}

operation ASM!EClass getExposedMappedTransferObjectScrXmlFileName(ap: ASM!EClass) : String {
	return "OSGI-INF/" + self.getExposedMappedTransferObjectPackageName(ap) + "." + self.getExposedMappedTransferObjectClassName() + ".xml";
}

operation String safeName() : String {
	if (self == "class") {
		return "clazz";
	} else if (Set{
	    "identifier",
		"abstract", "assert", "boolean", "break", "byte", "case", "catch", "char",
		"continue", "default", "do", "double", "else", "enum", "exports", "extends", 
		"final", "finally", "float", "for", "if", "implements", "import", "instanceof",
		 "long", "module", "native", "new", "package", "private", "protected",
		 "public", "requires", "return", "short", "static", "strictfp", "super",
		 "switch", "synchronized", "this", "throw", "throws", "transitent", "try",
		 "void", "volatile", "while", "true", "null", "false", "var", "const", "goto" }.contains(self)) {
		return self + "_";
	} else if (self == "Class") {
		return "Clazz";
	} else {
		return self;
	}	
}

operation ASM!ENamedElement setterName() : String {
	return "set" + self.name.firstToUpperCase().safeName();
}

operation ASM!ENamedElement getterName() : String {
	return "get" + self.name.firstToUpperCase().safeName();
}


