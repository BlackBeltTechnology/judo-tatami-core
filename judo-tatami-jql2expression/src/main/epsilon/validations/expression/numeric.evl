context EXPR!CountExpression {
    constraint Resolved {
        check: true
        message: "Unsupported expression: " + self
    } 
}

context EXPR!RoundExpression {
    constraint Resolved {
        check: true
        message: "Unsupported expression: " + self
    }
}

context EXPR!IntegerAritmeticExpression {
    constraint Resolved {
        check: true
        message: "Unsupported expression: " + self
    }
}

context EXPR!DecimalAritmeticExpression {
    constraint Resolved {
        check: true
        message: "Unsupported expression: " + self
    }
    
    critique IntegerAritmeticExpressionIsRecommended {
        guard: self.satisfiesAll("Resolved")
        
        check: not (self.left.isKindOf(EXPR!IntegerExpression) 
            and self.right.isKindOf(EXPR!IntegerExpression)
            and (self.operator == EXPR!DecimalOperator#add or self.operator == EXPR!DecimalOperator#substract or self.operator == EXPR!DecimalOperator#multiply))
        message: "Both arguments are integer so integer aritmetic expression is recommended in: " + self
    }
}

context EXPR!IntegerOppositeExpression {
    constraint Resolved {
        guard: self.expression.satisfiesAll("Resolved")
        
        check: true
        message: "Unsupported expression: " + self
    }
}

context EXPR!DecimalOppositeExpression {
    constraint Resolved {
        guard: self.expression.satisfiesAll("Resolved")
        
        check: true
        message: "Unsupported expression: " + self
    }
}

context EXPR!IntegerAttribute {
    constraint AttributeTypeIsInteger {
        guard: self.satisfiesAll("Resolved")
    
        check: modelAdapter.isInteger(self.getAttributeType(modelAdapter).get())
        message: "Attribute type of " + self.attributeName + " of object type " + self.objectExpression.getObjectType(modelAdapter).getName() + " is not integer"
    }
}

context EXPR!DecimalAttribute {
    constraint AttributeTypeIsDecimal {
        guard: self.satisfiesAll("Resolved")
    
        check: modelAdapter.isDecimal(self.getAttributeType(modelAdapter).get())
        message: "Attribute type of " + self.attributeName + " of object type " + self.objectExpression.getObjectType(modelAdapter).getName() + " is not decimal"
    }
}

context EXPR!IntegerAggregatedExpression {
    constraint Resolved {
        guard: self.collectionExpression.satisfiesAll("Resolved")
        
        check: self.getObjectType(modelAdapter).isDefined()
        message: "Attribute named " + self.attributeName + " of object type " + self.collectionExpression.getObjectType(modelAdapter).getName() + " not found"
    }
}

context EXPR!DecimalAggregatedExpression {
    constraint Resolved {
        guard: self.collectionExpression.satisfiesAll("Resolved")
        
        check: self.getObjectType(modelAdapter).isDefined()
        message: "Attribute named " + self.attributeName + " of object type " + self.collectionExpression.getObjectType(modelAdapter).getName() + " not found"
    }

    critique AttributeTypeIsDecimalOrOperationIsNotSupportedOnIntegers {
        guard: self.satisfiesAll("Resolved")
        
        check: not (self.expression.isKindOf(EXPR!IntegerExpression) and (self.operator == EXPR!DecimalAggregator#sum or self.operator == EXPR!DecimalAggregator#min or self.operator == EXPR!DecimalAggregator#max))
        message: "Attribute named " + self.attributeName + " of object type " + self.collectionExpression.getObjectType(modelAdapter).getName() + " is integer so integer aggregation is recommended in: " + self
    }
}

context EXPR!IntegerSwitchExpression {
    constraint Resolved {
        check: true
        message: "Unsupported expression: " + self
    }
    
    constraint TypeOfDefaultCaseIsInteger {
        guard: self.defaultExpression.isDefined() and self.defaultExpression.satisfiesAll("Resolved")
        
        check: self.defaultExpression.isKindOf(EXPR!IntegerExpression)
        message: "Type of default case expression is not integer: " + self.defaultExpression
    }
}

context EXPR!DecimalSwitchExpression {
    constraint Resolved {
        check: true
        message: "Unsupported expression: " + self
    }
    
    constraint TypeOfDefaultCaseIsNumeric {
        guard: self.defaultExpression.isDefined() and self.defaultExpression.satisfiesAll("Resolved")
        
        check: self.defaultExpression.isKindOf(EXPR!NumericExpression)
        message: "Type of default case expression is not numeric: " + self.defaultExpression
    }
}

context EXPR!SwitchCase {
    constraint TypeOfSwitchCaseIsInteger {
        guard: self.getSwitchExpression().satisfiesAll("Resolved") and self.getSwitchExpression().isKindOf(EXPR!IntegerExpression)
        
        check: self.expression.isKindOf(EXPR!IntegerExpression)
        message: "Type of switch case expression is not integer: " + self.expression
    }
    
    constraint TypeOfSwitchCaseIsNumeric {
        guard: self.getSwitchExpression().satisfiesAll("Resolved") and self.getSwitchExpression().isKindOf(EXPR!DecimalExpression)
        
        check: self.expression.isKindOf(EXPR!NumericExpression)
        message: "Type of switch case expression is not numeric: " + self.expression
    }
}

// TODO: IntegerAggregatedExpression -> fork is not allowed (by expression argument)
// TODO: DecimalAggregatedExpression -> fork is not allowed (by expression argument)
