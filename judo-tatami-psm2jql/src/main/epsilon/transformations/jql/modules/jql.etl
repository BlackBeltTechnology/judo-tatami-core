import "../../../operations/psm/namespace/_importNamespace.eol";
import "../../../operations/psm/data/_importData.eol";
import "../../../operations/psm/derived/_importDerived.eol";

@cached
operation JUDOPSM!ExpressionType getReferenceTypedElement() : JUDOPSM!ReferenceTypedElement {
	return JUDOPSM!ReferenceAccessor.all.selectOne(e | e.getterExpression == self or e.setterExpression == self);
}

@cached
operation JUDOPSM!ExpressionType getPrimitiveTypedElement() : JUDOPSM!PrimitiveTypedElement {
	return JUDOPSM!PrimitiveAccessor.all.selectOne(e | e.getterExpression == self or e.setterExpression == self);
}

@cached
operation JUDOPSM!ReferenceTypedElement getEntityType() : JUDOPSM!EntityType {
    var entityType = JUDOPSM!EntityType.all.selectOne(e | e.relations.contains(self));
    if (entityType.isDefined()) {
        return entityType;
    }
	return JUDOPSM!EntityType.all.selectOne(e | e.navigationProperties.contains(self));
}

@cached
operation JUDOPSM!PrimitiveTypedElement getEntityType() : JUDOPSM!EntityType {
    var entityType = JUDOPSM!EntityType.all.selectOne(e | e.attributes.contains(self));
    if (entityType.isDefined()) {
        return entityType;
    }
	return JUDOPSM!EntityType.all.selectOne(e | e.dataProperties.contains(self));
}

@greedy
rule ExtractExpression
  transform s : JUDOPSM!ExpressionType
  to t : JQLEXTRACT!ExtractedExpression {
  	var ref = s.getReferenceTypedElement();
  	var attr = s.getPrimitiveTypedElement();
  	
  	t.context = new JQLEXTRACT!Context;
  	t.binding = new JQLEXTRACT!Binding;
  	
  	t.jqlExpression = jqlParser.parse(s.expression);
}

@greedy
rule AttachSourceToExtractedReferenceExpression
  transform s : JUDOPSM!ExpressionType
  to t : JQLEXTRACT!Binding {
  	guard: s.getReferenceTypedElement().isDefined()
  	
  	var ref = s.getReferenceTypedElement();
  	t.featureName = ref.name;
  	
  	var entityType = ref.getEntityType();
  	if (entityType.isDefined()) {
	  	if (entityType.getNamespace().isDefined()) {
	  		t.packageName = entityType.getNamespace().toString();
	  	}
  		t.className = entityType.name;
  	}
  	
  	var roles = new Set;
  	if (ref.getterExpression == s) {
  		roles.add("getter"); 
  	}
  	if (ref.setterExpression == s) {
  		roles.add("setter"); 
  	}
  	t.role = roles.concat(" ");
  	
  	s.equivalent().binding = t;
}

@greedy
rule AttachSourceToExtractedPrimitiveExpression
  transform s : JUDOPSM!ExpressionType
  to t : JQLEXTRACT!Binding {
  	guard: s.getPrimitiveTypedElement().isDefined()
  	
  	var attr = s.getPrimitiveTypedElement();
  	t.featureName = attr.name;
  	
  	var entityType = attr.getEntityType();
  	if (entityType.isDefined()) {
	  	if (entityType.getNamespace().isDefined()) {
	  		t.packageName = entityType.getNamespace().toString();
	  	}
  		t.className = entityType.name;
  	}
  	
  	var roles = new Set;
  	if (attr.getterExpression == s) {
  		roles.add("getter"); 
  	}
  	if (attr.setterExpression == s) {
  		roles.add("setter"); 
  	}
  	t.role = roles.concat(" ");
  	
  	s.equivalent().binding = t;
}

@greedy
rule AttachSelfVariableToExtractedReferenceExpression
  transform s : JUDOPSM!ExpressionType
  to t : JQLEXTRACT!Variable {
  	guard: s.getReferenceTypedElement().isDefined() and s.getReferenceTypedElement().getEntityType().isDefined()
  	
  	var entityType = s.getReferenceTypedElement().getEntityType();
  	
  	t.name = "self";
  	t.collection = false;
  	if (entityType.getNamespace().isDefined()) {
  		t.typeNamespace = entityType.getNamespace().toString();
  	}
  	t.typeName = entityType.name;
  	
  	s.equivalent().context.variables.add(t);
}

@greedy
rule AttachSelfVariableToExtractedPrimitiveExpression
  transform s : JUDOPSM!ExpressionType
  to t : JQLEXTRACT!Variable {
    guard: s.getPrimitiveTypedElement().isDefined() and s.getPrimitiveTypedElement().getEntityType().isDefined()
  	
  	var entityType = s.getPrimitiveTypedElement().getEntityType();
  	
  	t.name = "self";
  	t.collection = false;
  	if (entityType.getNamespace().isDefined()) {
  		t.typeNamespace = entityType.getNamespace().toString();
  	}
  	t.typeName = entityType.name;
  	
  	s.equivalent().context.variables.add(t);
}
