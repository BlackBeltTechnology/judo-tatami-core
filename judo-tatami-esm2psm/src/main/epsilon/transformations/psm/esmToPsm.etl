import "modules/namespace/namespace.etl";

import "modules/type/type.etl";

import "modules/constraint/invariantConstraint.etl";

import "modules/data/entityType.etl";
import "modules/data/primitiveTypedElement.etl";
import "modules/data/referenceTypedElement.etl";
import "modules/data/sequence.etl";

import "modules/measure/measure.etl";

import "modules/derived/expressionType.etl";
import "modules/derived/primitiveAccessor.etl";
import "modules/derived/referenceAccessor.etl";

import "modules/service/transferObjectType.etl";
import "modules/service/transferAttribute.etl";
import "modules/service/transferObjectRelation.etl";

import "modules/service/operation.etl";

import "modules/actor/actorType.etl";

post {
    
    var ecoreUtil = new Native("org.eclipse.emf.ecore.util.EcoreUtil");
    
    for (esmTransferObject in ESM!TransferObjectType.all) {
    	var psmTransferObject : JUDOPSM!TransferObjectType = esmTransferObject.getPSMTransferObjectTypeEquivalent();
    	var psmOptionalTransferObject : JUDOPSM!TransferObjectType = esmTransferObject.getPSMOptionalEquivalent();
    	var psmQueryCustomizerTransferObject : JUDOPSM!TransferObjectType = esmTransferObject.equivalent("CreateQueryCustomizerType");
    	var psmQueryOrderingEnumTransferObject : JUDOPSM!EnumerationType = esmTransferObject.equivalent("CreateQueryOrderingEnumeration");

    	for (esmSubClass in esmUtils.getAllDescendantsOfTransferObjectType(esmTransferObject)) {
    		var psmSubClass : JUDOPSM!TransferObjectType = esmSubClass.getPSMTransferObjectTypeEquivalent();
    		var psmOptionalSubClass : JUDOPSM!TransferObjectType = esmSubClass.getPSMOptionalEquivalent();
    	
            for (a in psmTransferObject.attributes) {
                if (not psmSubClass.attributes.exists(attr | attr.name.equals(a.name))) {
                    
                    var transferAttribute : JUDOPSM!TransferAttribute = ecoreUtil.copy(a);
                    
                    psmSubClass.attributes.add(transferAttribute);
                    log.debug("Added attribute: [" + a.name + "] to subclass [" + psmSubClass.name + "] from [" + psmTransferObject.name + "]");
                    
                    if (a.binding.isDefined() and a.binding.isKindOf(JUDOPSM!StaticData) and esmSubClass.isMapped()) {
                    
                        var property = esmSubClass.mapping.target.getPSMEquivalent().getAllDataProperties()
                            .selectOne(p | p.name == "_" + a.name + "_" + esmUtils.getNamespaceElementFQName(esmTransferObject).replace("::","_"));
                        
                        if (property.isDefined()) {
                            transferAttribute.binding = property;
                        } else {
                            var binding : JUDOPSM!DataProperty = new JUDOPSM!DataProperty();
	                        binding.name = "_" + a.name + "_" + esmUtils.getNamespaceElementFQName(esmTransferObject).replace("::","_");
	                        binding.required = a.binding.required;
	                        binding.dataType = a.binding.dataType;
	                        binding.getterExpression = ecoreUtil.copy(a.binding.getterExpression);
	                        esmSubClass.mapping.target.getPSMEquivalent().dataProperties.add(binding);
	                        transferAttribute.binding = binding;
                        }
                    }
                }
            }
            
            for (a in psmOptionalTransferObject.attributes) {
                if (not psmOptionalSubClass.attributes.exists(attr | attr.name.equals(a.name))) {
                    
                    var transferAttribute : JUDOPSM!TransferAttribute = ecoreUtil.copy(a);
                    
                    psmOptionalSubClass.attributes.add(transferAttribute);
                    log.debug("Added attribute: [" + a.name + "] to optional subclass [" + psmSubClass.name + "] from [" + psmTransferObject.name + "]");
                    
                    if (a.binding.isDefined() and a.binding.isKindOf(JUDOPSM!StaticData) and esmSubClass.isMapped()) {
                        var binding : JUDOPSM!DataProperty = esmSubClass.mapping.target.getPSMEquivalent().dataProperties
                            .selectOne(p | p.name == "_" + a.name + "_" + esmUtils.getNamespaceElementFQName(esmTransferObject).replace("::","_"));
                        transferAttribute.binding = binding;
                    }
                }
            }
            
            for (r in psmTransferObject.relations) {
                if (not psmSubClass.relations.exists(rel | rel.name.equals(r.name))) {
                    
                    var transferObjectRelation : JUDOPSM!TransferObjectRelation = ecoreUtil.copy(r);
                    
                    psmSubClass.relations.add(transferObjectRelation);
                    log.debug("Added relation: [" + r.name + "] to subclass [" + psmSubClass.name + "] from [" + psmTransferObject.name + "]");
                    
                    if (r.binding.isDefined() and r.binding.isKindOf(JUDOPSM!StaticNavigation) and esmSubClass.isMapped()) {
                        
                        var property = esmSubClass.mapping.target.getPSMEquivalent().getAllNavigationProperties()
                            .selectOne(p | p.name == "_" + r.name + "_" + esmUtils.getNamespaceElementFQName(esmTransferObject).replace("::","_"));
                        
                        if (property.isDefined()) {
                            transferObjectRelation.binding = property;
                        } else {
                            var binding : JUDOPSM!NavigationProperty = new JUDOPSM!NavigationProperty();
	                        binding.name = "_" + r.name + "_" + esmUtils.getNamespaceElementFQName(esmTransferObject).replace("::","_");
	                        binding.target = r.binding.target;
	                        binding.getterExpression = ecoreUtil.copy(r.binding.getterExpression);
	                        binding.cardinality = ecoreUtil.copy(r.binding.cardinality);
	                        esmSubClass.mapping.target.getPSMEquivalent().navigationProperties.add(binding);
	                        transferObjectRelation.binding = binding;
                        }
                    }
                }
            }
            
            for (r in psmOptionalTransferObject.relations) {
                if (not psmOptionalSubClass.relations.exists(rel | rel.name.equals(r.name))) {
                    
                    var transferObjectRelation : JUDOPSM!TransferObjectRelation = ecoreUtil.copy(r);
                    
                    psmOptionalSubClass.relations.add(transferObjectRelation);
                    log.debug("Added relation: [" + r.name + "] to optional subclass [" + psmSubClass.name + "] from [" + psmTransferObject.name + "]");
                    
                    if (r.binding.isDefined() and r.binding.isKindOf(JUDOPSM!StaticNavigation) and esmSubClass.isMapped()) {
                            
                        var binding : JUDOPSM!NavigationProperty = esmSubClass.mapping.target.getPSMEquivalent().navigationProperties
                            .selectOne(p | p.name == "_" + r.name + "_" + esmUtils.getNamespaceElementFQName(esmTransferObject).replace("::","_"));
                        transferObjectRelation.binding = binding;
                    }
                }
            }
    		
    		var cloneableOperations = psmTransferObject.operations
    											.select(o | o.behaviour.isUndefined() or 
    												(o.behaviour.isDefined() and
														o.behaviour.behaviourType <> JUDOPSM!TransferOperationBehaviourType#REFRESH
														and o.behaviour.behaviourType <> JUDOPSM!TransferOperationBehaviourType#UPDATE_INSTANCE
														and o.behaviour.behaviourType <> JUDOPSM!TransferOperationBehaviourType#VALIDATE_UPDATE
														and o.behaviour.behaviourType <> JUDOPSM!TransferOperationBehaviourType#DELETE_INSTANCE
														and o.behaviour.behaviourType <> JUDOPSM!TransferOperationBehaviourType#GET_PRINCIPAL
														and o.behaviour.behaviourType <> JUDOPSM!TransferOperationBehaviourType#GET_METADATA
														and o.behaviour.behaviourType <> JUDOPSM!TransferOperationBehaviourType#GET_TEMPLATE
													)
												);
    		
    		for (o in cloneableOperations) {
    			if (not psmSubClass.operations.exists(op | op.name.equals(o.name))) {
    			    var clone = ecoreUtil.copy(o);
    			    if (o.isKindOf(JUDOPSM!UnboundOperation) and o.initializer) {
    			         clone.initializer = false;
    			    }
	    			psmSubClass.operations.add(clone);
	    			log.debug("Added operation: [" + o.name + "] to subclass [" + psmSubClass.name + "] from [" + psmTransferObject.name + "]");
	    		}
    		}

            var psmQueryCustomizerSubClass : JUDOPSM!TransferObjectType = esmSubClass.equivalent("CreateQueryCustomizerType");
            var psmQueryOrderingEnumSubClass : JUDOPSM!EnumerationType = esmSubClass.equivalent("CreateQueryOrderingEnumeration");

    		if (psmQueryCustomizerSubClass.isDefined() and psmQueryOrderingEnumSubClass.isDefined()) {
    		    if (psmQueryCustomizerTransferObject.isDefined() and psmQueryOrderingEnumTransferObject.isDefined()) {
    		        for (member in psmQueryOrderingEnumTransferObject.members) {
                        if (not psmQueryOrderingEnumSubClass.members.exists(m | m.name == member.name)) {
                            var clone = ecoreUtil.copy(member);
                            psmQueryOrderingEnumSubClass.members.add(clone);
                        }
                    }

                    for (relation in psmQueryCustomizerTransferObject.relations) {
                        if (not psmQueryCustomizerSubClass.relations.exists(r | r.name == relation.name)) {
                            var clone = ecoreUtil.copy(relation);
                            psmQueryCustomizerSubClass.relations.add(clone);
                        }
                    }
    		    }
    		}
    	}
	}
	
	for (o in JUDOPSM!TransferOperation.all.select(op | op.behaviour.isDefined())) {
		
		if (o.behaviour.owner.isKindOf(JUDOPSM!TransferObjectRelation)) {
			var newOwner : JUDOPSM!TransferObjectRelation = o.eContainer.relations.select(r | r.name.equals(o.behaviour.owner.name)).first();
			o.behaviour.owner = newOwner;
			
			if (o.behaviour.behaviourType == JUDOPSM!TransferOperationBehaviourType#GET_RANGE) {
			    var inputTypeOwner = o.input.type.relations.selectOne(r | r.name == "owner");
			    
			    if (inputTypeOwner.target <> o.eContainer.~source.getPSMOptionalEquivalent()) {
			         var newInputType = ecoreUtil.copy(o.input.type);
			         newInputType.name = "_GetRangeInput" + o.eContainer.~source.name.firstToUpperCase() + o.behaviour.owner.name.firstToUpperCase();
			         var newInputTypeOwner = newInputType.relations.selectOne(r | r.name == "owner");
                     newInputTypeOwner.target = o.eContainer.~source.getPSMOptionalEquivalent();
                     o.input.type.eContainer.elements.add(newInputType);
                     o.input.type = newInputType;
			    }
            }
		}
	}

    var enumsWithoutOrdinal = JUDOPSM!EnumerationMember.all.select(m | m.ordinal == -1).collect(m | m.eContainer).flatten().asSet();
    for (e in enumsWithoutOrdinal) {
        log.debug("Set/overwrite ordinals of enumeration: " + e.name);
        var index = 0;
        for (m in e.members) {
            m.ordinal = index;
            index = index + 1;
        }
    }
}
