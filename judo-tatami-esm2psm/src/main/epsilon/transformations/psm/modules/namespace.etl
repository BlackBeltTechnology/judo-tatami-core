import "../../../operations/esm/namespace/_importNamespace.eol";

@abstract
rule CreateNamedElement
  transform s : ESM!NamedElement
  to t : JUDOPSM!NamedElement {
    t.name = s.name;
}

@abstract
rule CreateNamespaceElement
  transform s : ESM!NamespaceElement
  to t : JUDOPSM!NamespaceElement
  extends CreateNamedElement {
    var namespace : ESM!Namespace = s.getNamespace();
    if (namespace.isKindOf(ESM!Model)) {
        namespace.equivalent("CreateModel").elements.add(t);
    } else if (namespace.isKindOf(ESM!Package)) {
        namespace.equivalent("CreatePackage").elements.add(t);
    }
    
    log.debug("Created namespace element: " + t.name);
}

rule CreateModel
  transform s : ESM!Model
  to t : JUDOPSM!Model
  extends CreateNamedElement {
    log.debug("Created model: " + t.name);
}

rule CreatePackage
  transform s : ESM!Package
  to t : JUDOPSM!Package
  extends CreateNamedElement {
    var namespace : ESM!Namespace = s.getNamespace();
    if (namespace.isKindOf(ESM!Model)) {
        namespace.equivalent("CreateModel").packages.add(t);
    } else if (namespace.isKindOf(ESM!Package)) {
        namespace.equivalent("CreatePackage").packages.add(t);
    }
    log.debug("Created namespace: " + t.name);
}