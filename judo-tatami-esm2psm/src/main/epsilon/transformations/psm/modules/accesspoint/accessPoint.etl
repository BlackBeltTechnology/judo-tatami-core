import "../namespace/namespace.etl";
import "../derived/referenceAccessor.etl";
import "../../../../operations/_importAll.eol";

@abstract
rule CreateAbstractActorType
	transform s: ESM!ActorType
	to t: JUDOPSM!AbstractActorType {
	  t.name = s.eContainer.name;

	  t.transferObjectType = s.eContainer.getPSMTransferObjectTypeEquivalent();
	  s.eContainer.getPSMTransferObjectTypeEquivalent().actorType = t;
	  s.eContainer.eContainer.getPSMGeneratedActorTypeNamespaceEquivalent().elements.add(t);

	  if (s.realm.isDefined() and s.realm.trim() <> '') {
          t.realm = s.realm;
	  }
}

rule CreateActorTypeClaim
    transform s : ESM!Claim
    to t : JUDOPSM!TransferAttribute
    extends CreateNamedElement {
        t.claimType = s.claimType.asString();
        t.dataType = s.attribute.dataType.getPSMEquivalent();

        if (s.attribute.binding.isDefined() and s.attribute.memberType == ESM!MemberType#MAPPED) {
            t.binding = s.attribute.equivalent("CreateTransferAttributeFromBound").binding;
        }

        s.eContainer.getPSMEquivalent().attributes.add(t);

        log.debug("Created ActorType Claim: " + t.name);
}

rule CreateActorType
	transform s: ESM!ActorType
	to t: JUDOPSM!ActorType
	extends CreateAbstractActorType {
	  guard: s.eContainer.isDefined() and not s.eContainer.mapping.isDefined()

	  log.debug("Created ActorType: " + t.name);
}

rule CreateMappedActorType
	transform s: ESM!ActorType
	to t: JUDOPSM!MappedActorType
	extends CreateAbstractActorType {
	  guard: s.eContainer.isDefined() and s.eContainer.mapping.isDefined()

	  t.entityType = s.eContainer.mapping.target.getPSMEquivalent();

	  log.debug("Created ActorType: " + t.name);
}

@greedy
rule CreateGetOperationForExposedGraph
    transform s : ESM!RelationFeature
    to t : JUDOPSM!UnboundOperation {
        guard: s.eContainer.isAccesspoint() and not s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.memberType == ESM!MemberType#DERIVED

        t.name = "_get" + s.name.firstToUpperCase();
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#GET;
        
	    t.behaviour.owner = s.getPSMTransferObjectRelationEquivalent();
        
        t.output = new JUDOPSM!Parameter;
        t.output.name = "output";
        t.output.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.output.cardinality = new JUDOPSM!Cardinality;
        t.output.cardinality.lower = s.lower;
        t.output.cardinality.upper = s.upper;

        s.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);
}

operation ESM!RelationFeature createSetOperationForExposedGraph(relation : ESM!RelationFeature) : JUDOPSM!UnboundOperation {
    var t = new JUDOPSM!UnboundOperation;

    t.name = "_set" + relation.name.firstToUpperCase() + "Of" + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#SET_RELATION;
    t.behaviour.owner = self.getPSMTransferObjectRelationEquivalent();
    
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

operation ESM!RelationFeature createUnsetOperationForExposedGraph(relation : ESM!RelationFeature) : JUDOPSM!UnboundOperation {
    var t = new JUDOPSM!UnboundOperation;

    t.name = "_unset" + relation.name.firstToUpperCase() + "Of" + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#UNSET_RELATION;
    t.behaviour.owner = self.getPSMTransferObjectRelationEquivalent();
    
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

operation ESM!RelationFeature createAddAllOperationForExposedGraph(relation : ESM!RelationFeature) : JUDOPSM!UnboundOperation {
    var t = new JUDOPSM!UnboundOperation;

    t.name = "_add" + relation.name.firstToUpperCase() + "To" + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION;
    t.behaviour.owner = self.getPSMTransferObjectRelationEquivalent();
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

operation ESM!RelationFeature createRemoveAllOperationForExposedGraph(relation : ESM!RelationFeature) : JUDOPSM!UnboundOperation {
    var t = new JUDOPSM!UnboundOperation;

    t.name = "_remove" + relation.name.firstToUpperCase() + "From" + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#REMOVE_ALL_FROM_RELATION;
    t.behaviour.owner = self.getPSMTransferObjectRelationEquivalent();
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

operation ESM!RelationFeature createGetRangeOperationForExposedGraphEdit(relation : ESM!RelationFeature, parameter : JUDOPSM!Parameter, action : String) : JUDOPSM!UnboundOperation {
    var t = new JUDOPSM!UnboundOperation;

    t.name = "_getRangeOf" + relation.name.firstToUpperCase() + "To" + action.firstToUpperCase() + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#GET_RANGE_OF_RELATION;
    t.behaviour.owner = parameter;
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 0;
    t.input.cardinality.upper = 1;

    t.output = new JUDOPSM!Parameter;
    t.output.name = "output";
    t.output.type = relation.target.getPSMTransferObjectTypeEquivalent();
    t.output.cardinality = new JUDOPSM!Cardinality;
    t.output.cardinality.lower = 0;
    t.output.cardinality.upper = -1;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

@greedy
rule CreateCreateOperationForExposedGraph
    transform s : ESM!RelationFeature
    to t : JUDOPSM!UnboundOperation {
        guard: s.eContainer.isAccesspoint() and not s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.isCreateAllowed() and s.memberType == ESM!MemberType#DERIVED

        t.name = "_create" + s.name.firstToUpperCase();
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#CREATE;
        t.behaviour.owner = s.getPSMTransferObjectRelationEquivalent();

        t.input = new JUDOPSM!Parameter;
        t.input.name = "input";
        t.input.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.input.cardinality = new JUDOPSM!Cardinality;
        t.input.cardinality.lower = 1;
        t.input.cardinality.upper = 1;

        t.output = new JUDOPSM!Parameter;
        t.output.name = "output";
        t.output.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.output.cardinality = new JUDOPSM!Cardinality;
        t.output.cardinality.lower = 1;
        t.output.cardinality.upper = 1;

        for (relation in s.target.getAllRelations().select(r | r.target.mapping.isDefined() and not r.isReadOnly() and (r.isKindOf(ESM!TwoWayRelationMember) implies r.partner <> s))) {
            var rangeAllowed = false;
            if (relation.isKindOf(ESM!OneWayRelationMember)) {
                if (relation.eContainer.isKindOf(ESM!EntityType)) {
                    rangeAllowed = not relation.isComposition() and relation.memberType == ESM!MemberType#STORED;
                } else if (relation.eContainer.isKindOf(ESM!TransferObjectType)) {
                    rangeAllowed = relation.memberType == ESM!MemberType#MAPPED and not relation.binding.isComposition() and relation.binding.memberType == ESM!MemberType#STORED;
                }
            } else if (relation.isKindOf(ESM!TwoWayRelationMember)) {
                rangeAllowed = true;
            }

            if (rangeAllowed and relation.rangeExpression.isDefined() and relation.rangeExpression.trim().length() > 0) {
                s.createGetRangeOperationForExposedGraphEdit(relation, t.input, "create");
            }
        }

        s.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);
}

@greedy
rule CreateUpdateOperationForExposedGraph
    transform s : ESM!RelationFeature
    to t : JUDOPSM!UnboundOperation {
        guard: s.eContainer.isAccesspoint() and not s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.isUpdateAllowed() and s.memberType == ESM!MemberType#DERIVED

        t.name = "_update" + s.name.firstToUpperCase();
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#UPDATE;
        t.behaviour.owner = s.getPSMTransferObjectRelationEquivalent();
        

        t.input = new JUDOPSM!Parameter;
        t.input.name = "input";
        t.input.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.input.cardinality = new JUDOPSM!Cardinality;
        t.input.cardinality.lower = 1;
        t.input.cardinality.upper = 1;

        t.output = new JUDOPSM!Parameter;
        t.output.name = "output";
        t.output.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.output.cardinality = new JUDOPSM!Cardinality;
        t.output.cardinality.lower = 1;
        t.output.cardinality.upper = 1;

        for (relation in s.target.getAllRelations().select(r | r.target.mapping.isDefined() and not r.isReadOnly() and (r.isKindOf(ESM!TwoWayRelationMember) implies r.partner <> s))) {
            var rangeAllowed = false;
            var containment = false;
            if (relation.isKindOf(ESM!OneWayRelationMember)) {
                if (relation.eContainer.isKindOf(ESM!EntityType)) {
                    rangeAllowed = not relation.isComposition() and relation.memberType == ESM!MemberType#STORED;
                    containment = relation.isComposition();
                } else if (relation.eContainer.isKindOf(ESM!TransferObjectType)) {
                    rangeAllowed = relation.memberType == ESM!MemberType#MAPPED and not relation.binding.isComposition() and relation.binding.memberType == ESM!MemberType#STORED;
                    containment = relation.memberType == ESM!MemberType#MAPPED and relation.binding.isComposition() and relation.target.mapping.isDefined();
                }
            } else if (relation.isKindOf(ESM!TwoWayRelationMember)) {
                rangeAllowed = true;
            }

            var entityPartner;
            if (relation.isKindOf(ESM!TwoWayRelationMember)) {
                entityPartner = relation.partner;
            } else if (relation.memberType == ESM!MemberType#MAPPED and relation.binding.isKindOf(ESM!TwoWayRelationMember)) {
                entityPartner = relation.binding.partner;
            }

            if (relation.upper == 1) {
                if (not containment and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createSetOperationForExposedGraph(relation);
                }
                if (relation.lower == 0 and (not containment or s.deleteable) and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createUnsetOperationForExposedGraph(relation);
                }
            } else {
                if (not containment and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createSetOperationForExposedGraph(relation);
                }
                if ((relation.lower < relation.upper or relation.upper == -1) and not containment and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createAddAllOperationForExposedGraph(relation);
                }
                if ((relation.lower < relation.upper or relation.upper == -1) and (not containment or s.deleteable) and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createRemoveAllOperationForExposedGraph(relation);
                }
            }

            if (rangeAllowed and relation.rangeExpression.isDefined() and relation.rangeExpression.trim().length() > 0) {
                s.createGetRangeOperationForExposedGraphEdit(relation, t.input, "update");
            }
        }

        s.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);
}

@greedy
rule CreateDeleteOperationForExposedGraph
    transform s : ESM!RelationFeature
    to t : JUDOPSM!UnboundOperation {
        guard: s.eContainer.isAccesspoint() and not s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.isDeleteAllowed() and s.memberType == ESM!MemberType#DERIVED

        t.name = "_delete" + s.name.firstToUpperCase();
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#DELETE;
        t.behaviour.owner = s.getPSMTransferObjectRelationEquivalent();

        t.input = new JUDOPSM!Parameter;
        t.input.name = "input";
        t.input.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.input.cardinality = new JUDOPSM!Cardinality;
        t.input.cardinality.lower = 1;
        t.input.cardinality.upper = 1;

        s.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);
        
}
