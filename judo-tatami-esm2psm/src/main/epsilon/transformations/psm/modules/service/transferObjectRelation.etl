import "../namespace/namespace.etl";
import "../data/primitiveTypedElement.etl";
import "../data/referenceTypedElement.etl";
import "../derived/expressionType.etl";
import "../../../../operations/_importAll.eol";

@abstract
rule CreateTransferObjectRelation
    transform s: ESM!RelationFeature
    to t: JUDOPSM!TransferObjectRelation
    extends CreateNamedElement {
    
      t.target = s.target.getPSMTransferObjectTypeEquivalent();
      t.embedded = s.isAggregation();
      
      t.cardinality = s.equivalent("CreateCardinalityForReferenceTypedElement");
      t.defaultValue = s.equivalent("CreateStaticNavigationForTransferObjectRelationDefaultValue");
      t.range = s.equivalent("CreateNavigationPropertyForTransferObjectRelationRange");
      
//      t.get = s.equivalent("CreateGetOperationForTransferObjectRelation");
//      t.create = s.equivalent("CreateCreateOperationForTransferObjectRelation");
//      t.update = s.equivalent("CreateUpdateOperationForTransferObjectRelation");
//      t.`delete` = s.equivalent("CreateDeleteOperationForTransferObjectRelation");
      
      s.eContainer.getPSMEquivalent().relations.add(t);
}

@greedy
rule CreateRangeTransferObjectRelation
    transform s : ESM!RelationFeature
    to t : JUDOPSM!TransferObjectRelation {
        guard: s.eContainer.isKindOf(ESM!TransferObjectType) and s.eContainer.mapping.isDefined()
            and s.target.mapping.isDefined() and s.rangeExpression.isDefined() and s.rangeExpression.trim().length() > 0

        t.name = s.equivalent("CreateNavigationPropertyForTransferObjectRelationRange").name;
        t.cardinality = new JUDOPSM!Cardinality;
        t.cardinality.lower = 0;
        t.cardinality.upper = -1;
        t.target = s.target.getPSMTransferObjectTypeEquivalent();
        t.binding = s.equivalent("CreateNavigationPropertyForTransferObjectRelationRange");

        s.eContainer.getPSMTransferObjectTypeEquivalent().relations.add(t);
}

rule CreateTransferObjectRelationFromRelationOrProperty
    transform s: ESM!OneWayRelationMember
    to t: JUDOPSM!TransferObjectRelation
    extends CreateTransferObjectRelation {
      guard: s.eContainer.isKindOf(ESM!TransferObjectType) 
            and not s.eContainer.isTypeOf(ESM!EntityType)
            and s.memberType <> ESM!MemberType#MAPPED

      if(s.eContainer.isMapped()) {
      	t.binding = s.equivalent("CreateNavigationPropertyForTransferObjectRelationBinding");
      } else {
      	t.binding = s.equivalent("CreateStaticNavigationForExposedGraph");
      }
        
      log.debug("Created TransferObjectRelation (from OneWayRelationMember): [" + t.name + "] into [" + t.eContainer.name + "]");
}

rule CreateTransferObjectRelationFromBoundToOneWayRelationMember
    transform s: ESM!OneWayRelationMember
    to t: JUDOPSM!TransferObjectRelation
    extends CreateTransferObjectRelation {
      guard: s.memberType == ESM!MemberType#MAPPED
            and s.binding.isKindOf(ESM!OneWayRelationMember)
            
      if (s.binding.memberType = ESM!MemberType#STORED and s.binding.isComposition()) {
        t.binding = s.binding.equivalent("CreateContainment");
	        if (s.isCreateAllowed()) {
	            t.embeddedCreate = true;
	        }
	        
	        if (s.isUpdateAllowed()) {
	            t.embeddedUpdate = true;
	        }
	        
	        if (s.isDeleteAllowed()) {
	            t.embeddedDelete = true;
	        }
      } else if (s.binding.memberType = ESM!MemberType#STORED and not s.binding.isComposition()) {
        t.binding = s.binding.equivalent("CreateAssociationEndWithoutPartner");
      } else if (s.binding.memberType = ESM!MemberType#DERIVED) {
        t.binding = s.binding.equivalent("CreateNavigationProperty");
      }
     
      log.debug("Created TransferObjectRelation (from OneWayRelationMember): [" + t.name + "] into [" + t.eContainer.name + "]");
}

rule CreateTransferObjectRelationFromBoundToTwoWayRelationMember
    transform s: ESM!OneWayRelationMember
    to t: JUDOPSM!TransferObjectRelation
    extends CreateTransferObjectRelation {
      guard: s.memberType == ESM!MemberType#MAPPED
            and s.binding.isKindOf(ESM!TwoWayRelationMember)
            
        t.binding = s.binding.equivalent("CreateAssociationEndWithPartner");

      log.debug("Created TransferObjectRelation (from OneWayRelationMember): [" + t.name + "] into [" + t.eContainer.name + "]");
}

@lazy
@greedy
rule CreateCardinalityOfTransferObjectRelationForDefaultTransferObjectType
    transform s: ESM!ReferenceTypedElement
    to t: JUDOPSM!Cardinality {
      t.upper = s.upper;
      t.lower = s.lower;
}

@abstract
rule CreateTransferObjectRelationForDefaultTransferObjectType
    transform s: ESM!ReferenceTypedElement
    to t: JUDOPSM!TransferObjectRelation
    extends CreateNamedElement {
      t.cardinality = s.equivalent("CreateCardinalityOfTransferObjectRelationForDefaultTransferObjectType");
      t.target = s.target.equivalent("CreateDefaultMappedTransferObjectTypeForEntity");
      s.eContainer.equivalent("CreateDefaultMappedTransferObjectTypeForEntity").relations.add(t);
      t.defaultValue = s.equivalent("CreateStaticNavigationForTransferObjectRelationDefaultValue");
      t.range = s.equivalent("CreateNavigationPropertyForTransferObjectRelationRange");
      t.embedded = s.isAggregation();
      
//      t.get = s.equivalent("CreateGetOperationForTransferObjectRelation");
//      t.create = s.equivalent("CreateCreateOperationForTransferObjectRelation");
//      t.update = s.equivalent("CreateUpdateOperationForTransferObjectRelation");
//      t.`delete` = s.equivalent("CreateDeleteOperationForTransferObjectRelation");
      
      log.debug("Created TransferObjectRelation for DefaultTransferObjectType: [" + t.name + "] into [" + t.eContainer.name + "]");
}

rule CreateTransferObjectRelationForDefaultTransferObjectTypeFromOneWayRelationMemberContainment
    transform s: ESM!OneWayRelationMember
    to t: JUDOPSM!TransferObjectRelation
    extends CreateTransferObjectRelationForDefaultTransferObjectType {
      guard: s.memberType == ESM!MemberType#STORED
           and s.isComposition()
           
      t.embedded = s.isComposition();
      
      if (s.isCreateAllowed()) {
          t.embeddedCreate = true;
      }
      if (s.isUpdateAllowed()) {
          t.embeddedUpdate = true;
      }
      if (s.isDeleteAllowed()) {
          t.embeddedDelete = true;
      }

      t.binding = s.equivalent("CreateContainment");
      
      log.debug("Created TransferObjectRelation for DefaultTransferObjectType: [" + t.name + "] into [" + t.eContainer.name + "]");
}

rule CreateTransferObjectRelationForDefaultTransferObjectTypeFromOneWayRelationMemberNotContainment
    transform s: ESM!OneWayRelationMember
    to t: JUDOPSM!TransferObjectRelation
    extends CreateTransferObjectRelationForDefaultTransferObjectType {
      guard: s.memberType == ESM!MemberType#STORED
           and not s.isComposition()
           
      t.binding = s.equivalent("CreateAssociationEndWithoutPartner");
      
      log.debug("Created TransferObjectRelationFromTwoWayRelationMember: [" + t.name + "] into [" + t.eContainer.name + "]");
}

rule CreateTransferObjectRelationForDefaultTransferObjectTypeFromOneWayRelationMemberProperty
    transform s: ESM!OneWayRelationMember
    to t: JUDOPSM!TransferObjectRelation
    extends CreateTransferObjectRelationForDefaultTransferObjectType {
      guard: s.eContainer.isKindOf(ESM!EntityType)
           and s.memberType == ESM!MemberType#DERIVED
           
      t.binding = s.equivalent("CreateNavigationProperty");
      
      log.debug("Created TransferObjectRelationFromTwoWayRelationMember: [" + t.name + "] into [" + t.eContainer.name + "]");
}

rule CreateTransferObjectRelationForDefaultTransferObjectTypeFromTwoWayRelationMember
    transform s: ESM!TwoWayRelationMember
    to t: JUDOPSM!TransferObjectRelation
    extends CreateTransferObjectRelationForDefaultTransferObjectType {
      
      t.binding = s.equivalent("CreateAssociationEndWithPartner");
      
      log.debug("Created TransferObjectRelationFromTwoWayRelationMember: [" + t.name + "] into [" + t.eContainer.name + "]");
}
