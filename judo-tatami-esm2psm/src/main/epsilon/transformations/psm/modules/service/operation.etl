import "../namespace/namespace.etl";
import "../../../../operations/_importAll.eol";

@lazy
@greedy
rule CreateOperationBody
    transform s: ESM!Operation
    to t: JUDOPSM!OperationBody {
      t.body = s.body;
      t.stateful = s.stateful;
      t.customImplementation = s.customImplementation and s.operationType <> ESM!OperationType#ABSTRACT;
}

rule CreateBoundOperation
    transform s: ESM!Operation
    to t: JUDOPSM!BoundOperation
    extends CreateNamedElement {
      guard: s.operationType <> ESM!OperationType#STATIC and s.eContainer.isKindOf(ESM!EntityType)

      t.`abstract` = s.operationType == ESM!OperationType#ABSTRACT;

      if (s.operationType <> ESM!OperationType#ABSTRACT) {
      	t.implementation = s.equivalent("CreateOperationBody");
      }

      t.instanceRepresentation = s.eContainer.equivalent("CreateDefaultMappedTransferObjectTypeForEntity");
      s.eContainer.equivalent("CreateEntityType").operations.add(t);
}

rule CreateBoundTransferOperationInDefaultTransferObjectType
    transform s: ESM!Operation
    to t: JUDOPSM!BoundTransferOperation
    extends CreateNamedElement {
      guard: s.operationType <> ESM!OperationType#STATIC and s.eContainer.isKindOf(ESM!EntityType)

      t.binding = s.equivalent("CreateBoundOperation");

      if (s.input.isDefined()) {
          t.input = new JUDOPSM!Parameter;
          t.input.name = s.input.equivalent("CreateParameter").name;
          t.input.type = s.input.equivalent("CreateParameter").type;
          t.input.cardinality = new JUDOPSM!Cardinality;
          t.input.cardinality.lower = s.input.lower;
          t.input.cardinality.upper = s.input.upper;
      }

      if (s.output.isDefined()) {
          t.output = new JUDOPSM!Parameter;
          t.output.name = s.output.equivalent("CreateParameter").name;
          t.output.type = s.output.equivalent("CreateParameter").type;
          t.output.cardinality = new JUDOPSM!Cardinality;
          t.output.cardinality.lower = s.output.lower;
          t.output.cardinality.upper = s.output.upper;
      }

      for (f in s.faults) {
          var fault = new JUDOPSM!Parameter;
          fault.name = f.equivalent("CreateParameter").name;
          fault.type = f.equivalent("CreateParameter").type;
          fault.cardinality = new JUDOPSM!Cardinality;
          fault.cardinality.lower = 1;
          fault.cardinality.upper = 1;
          t.faults.add(fault);
      }

      s.eContainer.equivalent("CreateDefaultMappedTransferObjectTypeForEntity").operations.add(t);
}

rule CreateBoundTransferOperation
    transform s: ESM!Operation
    to t: JUDOPSM!BoundTransferOperation
    extends CreateNamedElement {
      guard: s.operationType == ESM!OperationType#MAPPED

      var resolvedBinding = esmUtils.resolveBindingOfOperation(s.eContainer, s).orElse(null);
      if (resolvedBinding.isDefined()) {
          t.binding = resolvedBinding.equivalent("CreateBoundOperation");
      }
      s.eContainer.getPSMEquivalent().operations.add(t);
}

rule CreateUnboundTransferOperationInDefaultTransferObjectType
    transform s: ESM!Operation
    to t: JUDOPSM!UnboundOperation
    extends CreateNamedElement {
      guard: s.operationType == ESM!OperationType#STATIC and s.eContainer.isKindOf(ESM!EntityType)

      t.implementation = s.equivalent("CreateOperationBody");
      t.initializer = s.initializer;

      s.eContainer.equivalent("CreateDefaultMappedTransferObjectTypeForEntity").operations.add(t);
}

rule CreateInPlaceUnboundOperation
    transform s: ESM!Operation
    to t: JUDOPSM!UnboundOperation
    extends CreateNamedElement {
	  guard: s.operationType == ESM!OperationType#STATIC and not s.eContainer.isKindOf(ESM!EntityType)

	  t.implementation = s.equivalent("CreateOperationBody");
	  t.initializer = s.initializer;

	  s.eContainer.getPSMEquivalent().operations.add(t);
}

@lazy
@greedy
rule CreateCardinalityForParameter
	transform s: ESM!Parameter
	to t: JUDOPSM!Cardinality {
	  t.upper = s.upper;
  	  t.lower = s.lower;
}

rule CreateParameter
	transform s : ESM!Parameter
	to t : JUDOPSM!Parameter
	extends CreateNamedElement {

	  t.cardinality = s.equivalent("CreateCardinalityForParameter");

	  t.type = s.target.getPSMTransferObjectTypeEquivalent();

	  if (s.eContainer.input == s) {
	  	s.eContainer.getPSMEquivalent().input = t;
	  } else if (s.eContainer.output == s) {
	  	s.eContainer.getPSMEquivalent().output = t;
	  } else if (s.eContainer.faults.contains(s)) {
	  	s.eContainer.getPSMEquivalent().faults.add(t);
	  }
}

@greedy
rule CreateGetOperationForRelation
    transform s : ESM!RelationFeature
    to t : JUDOPSM!BoundOperation {
        guard: s.eContainer.isKindOf(ESM!TransferObjectType) and s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.memberType <> ESM!MemberType#TRANSIENT

        t.name = "_get" + s.name.firstToUpperCase() + "For" + esmUtils.getNamespaceElementFQName(s.eContainer).replace("::", "_").firstToUpperCase();
        t.instanceRepresentation = s.eContainer.getPSMTransferObjectTypeEquivalent();

        t.output = new JUDOPSM!Parameter;
        t.output.name = "output";
        t.output.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.output.cardinality = new JUDOPSM!Cardinality;
        t.output.cardinality.lower = s.lower;
        t.output.cardinality.upper = s.upper;

        s.eContainer.mapping.target.equivalent("CreateEntityType").operations.add(t);
}

@greedy
rule CreateGetTransferOperationForRelation
    transform s : ESM!RelationFeature
    to t : JUDOPSM!BoundTransferOperation {
        guard: s.eContainer.isKindOf(ESM!TransferObjectType) and s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.memberType <> ESM!MemberType#TRANSIENT

        t.name = "_get" + s.name.firstToUpperCase();
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#GET_RELATION;
        t.behaviour.owner = s.getPSMTransferObjectRelationEquivalent();

        t.output = new JUDOPSM!Parameter;
        t.output.name = "output";
        t.output.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.output.cardinality = new JUDOPSM!Cardinality;
        t.output.cardinality.lower = s.lower;
        t.output.cardinality.upper = s.upper;

        t.binding = s.equivalent("CreateGetOperationForRelation");

        s.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);
}

operation ESM!RelationFeature createSetOperationForRelation(relation : ESM!RelationFeature) : JUDOPSM!BoundOperation {
    var t = new JUDOPSM!BoundOperation;

    t.name = "_set" + relation.name.firstToUpperCase() + "Of" + self.name.firstToUpperCase() + "For" + esmUtils.getNamespaceElementFQName(self.eContainer).replace("::", "_").firstToUpperCase();
    t.instanceRepresentation = self.eContainer.getPSMTransferObjectTypeEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    self.eContainer.mapping.target.equivalent("CreateEntityType").operations.add(t);

    return t;
}

operation ESM!RelationFeature createSetTransferOperationForRelation(relation : ESM!RelationFeature, binding : JUDOPSM!BoundOperation) : JUDOPSM!BoundTransferOperation {
    var t = new JUDOPSM!BoundTransferOperation;

    t.name = "_set" + relation.name.firstToUpperCase() + "Of" + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#SET_RELATION_OF_RELATION;
    t.behaviour.owner = self.getPSMTransferObjectRelationEquivalent();
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    t.binding = binding;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

operation ESM!RelationFeature createUnsetOperationForRelation(relation : ESM!RelationFeature) : JUDOPSM!BoundOperation {
    var t = new JUDOPSM!BoundOperation;

    t.name = "_unset" + relation.name.firstToUpperCase() + "Of" + self.name.firstToUpperCase() + "For" + esmUtils.getNamespaceElementFQName(self.eContainer).replace("::", "_").firstToUpperCase();
    t.instanceRepresentation = self.eContainer.getPSMTransferObjectTypeEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    self.eContainer.mapping.target.equivalent("CreateEntityType").operations.add(t);

    return t;
}

operation ESM!RelationFeature createUnsetTransferOperationForRelation(relation : ESM!RelationFeature, binding : JUDOPSM!BoundOperation) : JUDOPSM!BoundTransferOperation {
    var t = new JUDOPSM!BoundTransferOperation;

    t.name = "_unset" + relation.name.firstToUpperCase() + "Of" + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#UNSET_RELATION_OF_RELATION;
    t.behaviour.owner = self.getPSMTransferObjectRelationEquivalent();
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    t.binding = binding;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

operation ESM!RelationFeature createAddAllOperationForRelation(relation : ESM!RelationFeature) : JUDOPSM!BoundOperation {
    var t = new JUDOPSM!BoundOperation;

    t.name = "_add" + relation.name.firstToUpperCase() + "To" + self.name.firstToUpperCase() + "For" + esmUtils.getNamespaceElementFQName(self.eContainer).replace("::", "_").firstToUpperCase();
    t.instanceRepresentation = self.eContainer.getPSMTransferObjectTypeEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    self.eContainer.mapping.target.equivalent("CreateEntityType").operations.add(t);

    return t;
}

operation ESM!RelationFeature createAddAllTransferOperationForRelation(relation : ESM!RelationFeature, binding : JUDOPSM!BoundOperation) : JUDOPSM!BoundTransferOperation {
    var t = new JUDOPSM!BoundTransferOperation;

    t.name = "_add" + relation.name.firstToUpperCase() + "To" + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION_OF_RELATION;
    t.behaviour.owner = self.getPSMTransferObjectRelationEquivalent();
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    t.binding = binding;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

operation ESM!RelationFeature createRemoveAllOperationForRelation(relation : ESM!RelationFeature) : JUDOPSM!BoundOperation {
    var t = new JUDOPSM!BoundOperation;

    t.name = "_remove" + relation.name.firstToUpperCase() + "From" + self.name.firstToUpperCase() + "For" + esmUtils.getNamespaceElementFQName(self.eContainer).replace("::", "_").firstToUpperCase();
    t.instanceRepresentation = self.eContainer.getPSMTransferObjectTypeEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    self.eContainer.mapping.target.equivalent("CreateEntityType").operations.add(t);

    return t;
}

operation ESM!RelationFeature createRemoveAllTransferOperationForRelation(relation : ESM!RelationFeature, binding : JUDOPSM!BoundOperation) : JUDOPSM!BoundTransferOperation {
    var t = new JUDOPSM!BoundTransferOperation;

    t.name = "_remove" + relation.name.firstToUpperCase() + "From" + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#REMOVE_ALL_FROM_RELATION_OF_RELATION;
    t.behaviour.owner = self.getPSMTransferObjectRelationEquivalent();
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 1;
    t.input.cardinality.upper = 1;

    t.binding = binding;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

operation ESM!RelationFeature createGetRangeTransferOperationForRelation(relation : ESM!RelationFeature, parameter : JUDOPSM!Parameter, action : String) : JUDOPSM!UnboundOperation {
    var t = new JUDOPSM!UnboundOperation;

    t.name = "_getRangeOf" + relation.name.firstToUpperCase() + "To" + action.firstToUpperCase() + self.name.firstToUpperCase();
    t.behaviour = new JUDOPSM!TransferOperationBehaviour;
    t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#GET_RANGE_OF_RELATION;
    t.behaviour.owner = parameter;
    t.behaviour.relation = relation.getPSMTransferObjectRelationEquivalent();

    t.input = new JUDOPSM!Parameter;
    t.input.name = "input";
    t.input.type = self.target.getPSMTransferObjectTypeEquivalent();
    t.input.cardinality = new JUDOPSM!Cardinality;
    t.input.cardinality.lower = 0;
    t.input.cardinality.upper = 1;

    t.output = new JUDOPSM!Parameter;
    t.output.name = "output";
    t.output.type = relation.target.getPSMTransferObjectTypeEquivalent();
    t.output.cardinality = new JUDOPSM!Cardinality;
    t.output.cardinality.lower = 0;
    t.output.cardinality.upper = -1;

    self.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);

    return t;
}

@greedy
rule CreateCreateOperationForRelation
    transform s : ESM!RelationFeature
    to t : JUDOPSM!BoundOperation {
        guard: s.eContainer.isKindOf(ESM!TransferObjectType) and s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.isCreateAllowed() and s.memberType <> ESM!MemberType#TRANSIENT

        t.name = "_create" + s.name.firstToUpperCase() + "For" + esmUtils.getNamespaceElementFQName(s.eContainer).replace("::", "_").firstToUpperCase();
        t.instanceRepresentation = s.eContainer.getPSMTransferObjectTypeEquivalent();

        t.input = new JUDOPSM!Parameter;
        t.input.name = "input";
        t.input.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.input.cardinality = new JUDOPSM!Cardinality;
        t.input.cardinality.lower = 1;
        t.input.cardinality.upper = 1;

        t.output = new JUDOPSM!Parameter;
        t.output.name = "output";
        t.output.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.output.cardinality = new JUDOPSM!Cardinality;
        t.output.cardinality.lower = 1;
        t.output.cardinality.upper = 1;

        s.eContainer.mapping.target.equivalent("CreateEntityType").operations.add(t);
}

@greedy
rule CreateCreateTransferOperationForRelation
    transform s : ESM!RelationFeature
    to t : JUDOPSM!BoundTransferOperation {
        guard: s.eContainer.isKindOf(ESM!TransferObjectType) and s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.isCreateAllowed() and s.memberType <> ESM!MemberType#TRANSIENT

        t.name = "_create" + s.name.firstToUpperCase();
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#CREATE_RELATION;
        t.behaviour.owner = s.getPSMTransferObjectRelationEquivalent();

        t.input = new JUDOPSM!Parameter;
        t.input.name = "input";
        t.input.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.input.cardinality = new JUDOPSM!Cardinality;
        t.input.cardinality.lower = 1;
        t.input.cardinality.upper = 1;

        t.output = new JUDOPSM!Parameter;
        t.output.name = "output";
        t.output.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.output.cardinality = new JUDOPSM!Cardinality;
        t.output.cardinality.lower = 1;
        t.output.cardinality.upper = 1;

        t.binding = s.equivalent("CreateCreateOperationForRelation");

        for (relation in s.target.getAllRelations().select(r | r.target.mapping.isDefined() and not r.isReadOnly() and (r.isKindOf(ESM!TwoWayRelationMember) implies r.partner <> s))) {
            var rangeAllowed = false;
            if (relation.isKindOf(ESM!OneWayRelationMember)) {
                if (relation.eContainer.isKindOf(ESM!EntityType)) {
                    rangeAllowed = not relation.isComposition() and relation.memberType == ESM!MemberType#STORED;
                } else if (relation.eContainer.isKindOf(ESM!TransferObjectType)) {
                    rangeAllowed = relation.memberType == ESM!MemberType#MAPPED and not relation.binding.isComposition() and relation.binding.memberType == ESM!MemberType#STORED;
                }
            } else if (relation.isKindOf(ESM!TwoWayRelationMember)) {
                rangeAllowed = true;
            }

            if (rangeAllowed and relation.rangeExpression.isDefined() and relation.rangeExpression.trim().length() > 0) {
                s.createGetRangeTransferOperationForRelation(relation, t.input, "create");
            }
        }

        s.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);
}

@greedy
rule CreateUpdateOperationForRelation
    transform s : ESM!RelationFeature
    to t : JUDOPSM!BoundOperation {
        guard: s.eContainer.isKindOf(ESM!TransferObjectType) and s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.isUpdateAllowed() and s.memberType <> ESM!MemberType#TRANSIENT

        t.name = "_update" + s.name.firstToUpperCase() + "For" + esmUtils.getNamespaceElementFQName(s.eContainer).replace("::", "_").firstToUpperCase();
        t.instanceRepresentation = s.eContainer.getPSMTransferObjectTypeEquivalent();

        t.input = new JUDOPSM!Parameter;
        t.input.name = "input";
        t.input.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.input.cardinality = new JUDOPSM!Cardinality;
        t.input.cardinality.lower = 1;
        t.input.cardinality.upper = 1;

        t.output = new JUDOPSM!Parameter;
        t.output.name = "output";
        t.output.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.output.cardinality = new JUDOPSM!Cardinality;
        t.output.cardinality.lower = 1;
        t.output.cardinality.upper = 1;

        s.eContainer.mapping.target.equivalent("CreateEntityType").operations.add(t);
}

@greedy
rule CreateUpdateTransferOperationForRelation
    transform s : ESM!RelationFeature
    to t : JUDOPSM!BoundTransferOperation {
        guard: s.eContainer.isKindOf(ESM!TransferObjectType) and s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.isUpdateAllowed() and s.memberType <> ESM!MemberType#TRANSIENT

        t.name = "_update" + s.name.firstToUpperCase();
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#UPDATE_RELATION;
        t.behaviour.owner = s.getPSMTransferObjectRelationEquivalent();

        t.input = new JUDOPSM!Parameter;
        t.input.name = "input";
        t.input.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.input.cardinality = new JUDOPSM!Cardinality;
        t.input.cardinality.lower = 1;
        t.input.cardinality.upper = 1;

        t.output = new JUDOPSM!Parameter;
        t.output.name = "output";
        t.output.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.output.cardinality = new JUDOPSM!Cardinality;
        t.output.cardinality.lower = 1;
        t.output.cardinality.upper = 1;

        t.binding = s.equivalent("CreateUpdateOperationForRelation");

        for (relation in s.target.getAllRelations().select(r | r.target.mapping.isDefined() and not r.isReadOnly() and (r.isKindOf(ESM!TwoWayRelationMember) implies r.partner <> s))) {
            var rangeAllowed = false;
            var containment = false;
            if (relation.isKindOf(ESM!OneWayRelationMember)) {
                if (relation.eContainer.isKindOf(ESM!EntityType)) {
                    rangeAllowed = not relation.isComposition() and relation.memberType == ESM!MemberType#STORED;
                    containment = relation.isComposition();
                } else if (relation.eContainer.isKindOf(ESM!TransferObjectType)) {
                    rangeAllowed = relation.memberType == ESM!MemberType#MAPPED and not relation.binding.isComposition() and relation.binding.memberType == ESM!MemberType#STORED;
                    containment = relation.memberType == ESM!MemberType#MAPPED and relation.binding.isComposition();
                }
            } else if (relation.isKindOf(ESM!TwoWayRelationMember)) {
                rangeAllowed = true;
            }

            var entityPartner;
            if (relation.isKindOf(ESM!TwoWayRelationMember)) {
                entityPartner = relation.partner;
            } else if (relation.memberType == ESM!MemberType#MAPPED and relation.binding.isKindOf(ESM!TwoWayRelationMember)) {
                entityPartner = relation.binding.partner;
            }

            if (relation.upper == 1) {
                if (not containment and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createSetTransferOperationForRelation(relation, s.createSetOperationForRelation(relation));
                }
                if (relation.lower == 0 and (not containment or s.isDeleteAllowed()) and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createUnsetTransferOperationForRelation(relation, s.createUnsetOperationForRelation(relation));
                }
            } else {
                if (not containment and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createSetTransferOperationForRelation(relation, s.createSetOperationForRelation(relation));
                }
                if ((relation.lower < relation.upper or relation.upper == -1) and not containment and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createAddAllTransferOperationForRelation(relation, s.createAddAllOperationForRelation(relation));
                }
                if ((relation.lower < relation.upper or relation.upper == -1) and (not containment or s.isDeleteAllowed()) and (entityPartner.isDefined() implies (entityPartner.lower < entityPartner.upper or entityPartner.upper == -1))) {
                    s.createRemoveAllTransferOperationForRelation(relation, s.createRemoveAllOperationForRelation(relation));
                }
            }

            if (rangeAllowed and relation.rangeExpression.isDefined() and relation.rangeExpression.trim().length() > 0) {
                s.createGetRangeTransferOperationForRelation(relation, t.input, "update");
            }
        }

        s.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);
}

@greedy
rule CreateDeleteOperationForRelation
    transform s : ESM!RelationFeature
    to t : JUDOPSM!BoundOperation {
        guard: s.eContainer.isKindOf(ESM!TransferObjectType) and s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.isDeleteAllowed() and s.memberType <> ESM!MemberType#TRANSIENT

        t.name = "_delete" + s.name.firstToUpperCase() + "For" + esmUtils.getNamespaceElementFQName(s.eContainer).replace("::", "_").firstToUpperCase();
        t.instanceRepresentation = s.eContainer.getPSMTransferObjectTypeEquivalent();

        t.input = new JUDOPSM!Parameter;
        t.input.name = "input";
        t.input.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.input.cardinality = new JUDOPSM!Cardinality;
        t.input.cardinality.lower = 1;
        t.input.cardinality.upper = 1;

        s.eContainer.mapping.target.equivalent("CreateEntityType").operations.add(t);
}

@greedy
rule CreateDeleteTransferOperationForRelation
    transform s : ESM!RelationFeature
    to t : JUDOPSM!BoundTransferOperation {
        guard: s.eContainer.isKindOf(ESM!TransferObjectType) and s.eContainer.mapping.isDefined() and s.target.isKindOf(ESM!TransferObjectType) and s.target.mapping.isDefined() and s.isDeleteAllowed() and s.memberType <> ESM!MemberType#TRANSIENT

        t.name = "_delete" + s.name.firstToUpperCase();
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#DELETE_RELATION;
        t.behaviour.owner = s.getPSMTransferObjectRelationEquivalent();

        t.input = new JUDOPSM!Parameter;
        t.input.name = "input";
        t.input.type = s.target.getPSMTransferObjectTypeEquivalent();
        t.input.cardinality = new JUDOPSM!Cardinality;
        t.input.cardinality.lower = 1;
        t.input.cardinality.upper = 1;

        t.binding = s.equivalent("CreateDeleteOperationForRelation");

        s.eContainer.getPSMTransferObjectTypeEquivalent().operations.add(t);
}

@greedy
rule CreateGetPrincipalOperationForAccessPoint
    transform s : ESM!TransferObjectType
    to t : JUDOPSM!UnboundOperation {
        guard: s.isAccessPoint()

        t.name = "_principal";
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#GET_PRINCIPAL;
        t.behaviour.owner = s.getPSMTransferObjectTypeEquivalent();

        s.getPSMTransferObjectTypeEquivalent().operations.add(t);
}

@greedy
rule CreateGetPrincipalOperationOutputParameterForAccessPoint
    transform s : ESM!TransferObjectType
    to t : JUDOPSM!Parameter {
        guard: s.isAccessPoint()

        t.name = "output";
        t.type = s.getPSMTransferObjectTypeEquivalent();
        t.cardinality = new JUDOPSM!Cardinality;
        t.cardinality.lower = 0;
        t.cardinality.upper = 1;

        s.equivalent("CreateGetPrincipalOperationForAccessPoint").output = t;
}

@greedy
rule CreateMapPrincipalOperationForActoryType
    transform s : ESM!ActorType
    to t : JUDOPSM!UnboundOperation {
        t.name = "_map_principal";
        t.behaviour = new JUDOPSM!TransferOperationBehaviour;
        t.behaviour.behaviourType = JUDOPSM!TransferOperationBehaviourType#MAP_PRINCIPAL;
        t.behaviour.owner = s.getPSMEquivalent();
        
        s.getPSMEquivalent().operations.add(t);
}

@greedy
rule CreateMapPrincipalOperationInputParameterForActorType
    transform s : ESM!ActorType
    to t : JUDOPSM!Parameter {
        guard: s.realm.isDefined() and s.realm.trim() <> ''

        t.name = "input";
        t.type = s.getPSMEquivalent();
        t.cardinality = new JUDOPSM!Cardinality;
        t.cardinality.lower = 0;
        t.cardinality.upper = 1;

        s.equivalent("CreateMapPrincipalOperationForActoryType").input = t;
}

@greedy
rule CreateMapPrincipalOperationOutputParameterForActorType
    transform s : ESM!ActorType
    to t : JUDOPSM!Parameter {
        guard: s.eContainer.isDefined()

        t.name = "output";
        t.type = s.eContainer.getPSMTransferObjectTypeEquivalent();
        t.cardinality = new JUDOPSM!Cardinality;
        t.cardinality.lower = 0;
        t.cardinality.upper = 1;

        s.equivalent("CreateMapPrincipalOperationForActoryType").output = t;
}
