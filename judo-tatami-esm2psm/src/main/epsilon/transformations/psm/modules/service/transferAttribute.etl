import "../namespace/namespace.etl";
import "../data/primitiveTypedElement.etl";
import "../data/referenceTypedElement.etl";
import "../derived/expressionType.etl";
import "../../../../operations/_importAll.eol";

rule CreateTransferAttributeFromAttribute
    transform s: ESM!DataMember
    to t: JUDOPSM!TransferAttribute 
    extends CreatePrimitiveTypedElement {
      guard: s.memberType == ESM!MemberType#TRANSIENT
            
      t.defaultValue = s.equivalent("CreateStaticDataForTransferAttributeDefaultValue");
      t.binding = s.equivalent("CreateDataPropertyForTransferAttributeBinding");
     
      s.eContainer.getPSMEquivalent().attributes.add(t);
      log.debug("Created TransferAttribute: [" + t.name + "] into [" + t.eContainer.name + "]");
}

rule CreateTransferAttributeFromBound
    transform s: ESM!DataMember
    to t: JUDOPSM!TransferAttribute 
    extends CreatePrimitiveTypedElement {
      guard: s.memberType == ESM!MemberType#MAPPED
            
      t.defaultValue = s.equivalent("CreateStaticDataForTransferAttributeDefaultValue");
      
      if (s.binding.memberType = ESM!MemberType#STORED){
         t.binding = s.binding.equivalent("CreateAttribute");
      } else if (s.binding.memberType = ESM!MemberType#DERIVED) {
         t.binding = s.binding.equivalent("CreateDataProperty");
      }
     
      s.eContainer.getPSMEquivalent().attributes.add(t);
      
      log.debug("Created TransferAttribute: [" + t.name + "] into [" + t.eContainer.name + "]");
}

rule CreateTransferAttributeFromProperty
    transform s: ESM!DataMember
    to t: JUDOPSM!TransferAttribute 
    extends CreatePrimitiveTypedElement {
      guard: s.eContainer.isKindOf(ESM!TransferObjectType)
            and not s.eContainer.isKindOf(ESM!EntityType) 
            and s.memberType == ESM!MemberType#DERIVED
            
      t.defaultValue = s.equivalent("CreateStaticDataForTransferAttributeDefaultValue");
      t.binding = s.equivalent("CreateDataPropertyForTransferAttributeBinding");
      
      s.eContainer.getPSMEquivalent().attributes.add(t);
      log.debug("Created TransferAttribute: [" + t.name + "] into [" + t.eContainer.name + "]");
}

//for default transfer object type
rule CreateTransferAttributeFromAttributeForDefaultTransferObjectType
    transform s: ESM!DataMember
    to t: JUDOPSM!TransferAttribute 
    extends CreatePrimitiveTypedElement {
      guard: s.memberType == ESM!MemberType#STORED
      
      t.defaultValue = s.equivalent("CreateStaticDataForTransferAttributeDefaultValue");
      t.binding = s.equivalent("CreateAttribute");
      
      s.eContainer.getPSMTransferObjectTypeEquivalent().attributes.add(t);
      log.debug("Created TransferAttribute for DefaultTransferObjectType: [" + t.name + "] into [" + t.eContainer.name + "]");
}

rule CreateTransferAttributeFromPropertyForDefaultTransferObjectType
    transform s: ESM!DataMember
    to t: JUDOPSM!TransferAttribute 
    extends CreatePrimitiveTypedElement {
      guard: s.eContainer.isKindOf(ESM!EntityType)
            and s.memberType == ESM!MemberType#DERIVED
      
      t.defaultValue = s.equivalent("CreateStaticDataForTransferAttributeDefaultValue");
      t.binding = s.equivalent("CreateDataProperty");
      
      s.eContainer.equivalent("CreateDefaultMappedTransferObjectTypeForEntity").attributes.add(t);
      log.debug("Created TransferAttribute for DefaultTransferObjectType: [" + t.name + "] into [" + t.eContainer.name + "]");
}
