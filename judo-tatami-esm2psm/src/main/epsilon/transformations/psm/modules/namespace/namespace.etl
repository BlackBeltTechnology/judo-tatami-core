import "../../../../operations/_importAll.eol";

@abstract
rule CreateNamedElement
  transform s : ESM!NamedElement
  to t : JUDOPSM!NamedElement {
    t.name = s.name;
}

@abstract
rule CreateNamespaceElement
  transform s : ESM!NamespaceElement
  to t : JUDOPSM!NamespaceElement
  extends CreateNamedElement {
    s.eContainer.getPSMEquivalent().elements.add(t);
    log.debug("Created namespace element: " + t.name);
}

rule CreateModel
  transform s : ESM!Model
  to t : JUDOPSM!Model
  extends CreateNamedElement {
    log.debug("Created model: " + t.name);

    for (claim in CLAIMMAPPING!Mapping.all
            .select(m | m.Claim.isDefined())
            .select(m | ESM!ActorType.all.exists(a | a.customRealm == m.Realm))) {
        var typeName = claim.getTypeName();
        var type;
        if (typeName.isDefined() and not t.elements.exists(e | e.name == typeName)) {
            switch (claim.Type) {
                case "String":
                    type = new JUDOPSM!StringType;
                    type.name = typeName;
                    if (claim.Size.isDefined()) {
                        type.maxLength = claim.Size.asFloat().asInteger();
                    } else {
                        type.maxLength = 255;
                    }
            }
        }
        if (type.isDefined()) {
            t.elements.add(type);
        }
    }
}

@lazy
rule CreateGeneratedNavigationRootPackage
  transform s : ESM!Model
  to t: JUDOPSM!Package {
    t.name = "_generated_navigations";
    s.equivalent("CreateModel").packages.add(t);
    log.debug("Created GeneratedNavigationPackage: " + t.name);
}

@lazy
rule CreateGeneratedDefaultTransferObjectTypeRootPackage
  transform s : ESM!Model
  to t: JUDOPSM!Package {
    t.name = "_default_transferobjecttypes";
    s.equivalent("CreateModel").packages.add(t);
    log.debug("Created DefaultTransferObjectTypePackage: " + t.name);
}

@lazy
rule CreateGeneratedDefaultTransferObjectTypePackage
  transform s : ESM!Package
  to t: JUDOPSM!Package {
    t.name = s.name;
    s.eContainer.getPSMGeneratedDefaultTransferObjectNamespaceEquivalent().packages.add(t);
    
    log.debug("Created Generated Package for DefaultTransferObjectType: " + t.name);
}

@lazy
rule CreateGeneratedActorTypeRootPackage
  transform s : ESM!Model
  to t: JUDOPSM!Package {
    t.name = "_actortypes";
    s.equivalent("CreateModel").packages.add(t);
    log.debug("Created ActorTypePackage: " + t.name);
}

@lazy
rule CreateGeneratedActorTypePackage
  transform s : ESM!Package
  to t: JUDOPSM!Package {
    t.name = s.name;
    s.eContainer.getPSMGeneratedActorTypeNamespaceEquivalent().packages.add(t);
    
    log.debug("Created Generated Package for DefaultTransferObjectType: " + t.name);
}

rule CreatePackage
  transform s : ESM!Package
  to t : JUDOPSM!Package
  extends CreateNamedElement {
    s.eContainer.getPSMEquivalent().packages.add(t);
    log.debug("Created namespace: " + t.name);
}
