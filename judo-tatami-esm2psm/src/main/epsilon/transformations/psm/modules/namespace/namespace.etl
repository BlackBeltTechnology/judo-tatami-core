import "../../../../operations/esm/namespace/namespace.eol";

@abstract
rule CreateNamedElement
  transform s : ESM!NamedElement
  to t : JUDOPSM!NamedElement {
    t.name = s.name;
}

@abstract
rule CreateNamespaceElement
  transform s : ESM!NamespaceElement
  to t : JUDOPSM!NamespaceElement
  extends CreateNamedElement {
    s.eContainer.getPSMEquivalent().elements.add(t);
    log.debug("Created namespace element: " + t.name);
}

rule CreateModel
  transform s : ESM!Model
  to t : JUDOPSM!Model
  extends CreateNamedElement {
    log.debug("Created model: " + t.name);
}

@lazy
rule CreateGeneratedNavigationPackage
  transform s : ESM!Model
  to t: JUDOPSM!Package {
    t.name = "_generated_navigations";
    s.equivalent("CreateModel").packages.add(t);
    log.debug("Created GeneratedNavigationPackage: " + t.name);
}

@lazy
rule CreateDefaultTransferObjectTypePackage
  transform s : ESM!Model
  to t: JUDOPSM!Package {
    t.name = "_default_transferobjecttypes";
    s.equivalent("CreateModel").packages.add(t);
    log.debug("Created DefaultTransferObjectTypePackage: " + t.name);
}

rule CreatePackage
  transform s : ESM!Package
  to t : JUDOPSM!Package
  extends CreateNamedElement {
    s.eContainer.getPSMEquivalent().packages.add(t);
    log.debug("Created namespace: " + t.name);
}
