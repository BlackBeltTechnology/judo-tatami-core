import "../data/referenceTypedElement.etl";
import "expressionType.etl";
import "../../../../operations/_importAll.eol";

@lazy
@greedy
rule CreateCardinalityForTransferObjectRelationBinding
    transform s: ESM!ReferenceTypedElement
    to t: JUDOPSM!Cardinality {
      t.upper = s.upper;
      t.lower = s.lower;
}

@lazy
@greedy
rule CreateCardinalityForTransferObjectRelationDefault
    transform s: ESM!ReferenceTypedElement
    to t: JUDOPSM!Cardinality {
      t.upper = s.upper;
      t.lower = 0;
}

@lazy
@greedy
rule CreateCardinalityForTransferObjectRelationRange
    transform s: ESM!ReferenceTypedElement
    to t: JUDOPSM!Cardinality {
	    t.lower = 0;
	    t.upper = -1;
}

rule CreateNavigationProperty
    transform s : ESM!OneWayRelationMember
    to t : JUDOPSM!NavigationProperty
    extends CreateNamedElement {
      guard: s.eContainer.isKindOf(ESM!EntityType)
            and s.memberType == ESM!MemberType#DERIVED
      
       t.cardinality = s.equivalent("CreateCardinalityForReferenceTypedElement");
       s.eContainer.getPSMEquivalent().navigationProperties.add(t);
       t.getterExpression = s.equivalent("CreateGetterExpressionForNavigationProperty");
       t.target = s.target.getPSMEquivalent();
       
       log.debug("Created Navigation Property: " + t.name);
}

rule CreateStaticNavigation
    transform s : ESM!StaticNavigation
    to t : JUDOPSM!StaticNavigation
    extends CreateNamedElement {

        t.cardinality = s.equivalent("CreateCardinalityForReferenceTypedElement");
        t.getterExpression = s.equivalent("CreateGetterExpressionForStaticNavigation");
        s.eContainer.getPSMEquivalent().elements.add(t);
        t.target = s.target.getPSMEquivalent();
        
        log.debug("Created Static Navigation: " + t.name);
}

//owner is transfer object type
@lazy
rule CreateNavigationPropertyForTransferObjectRelationBinding
    transform s : ESM!OneWayRelationMember
    to t : JUDOPSM!NavigationProperty
    extends CreateNamedElement {
        guard:  s.eContainer.mapping.isDefined()
                and s.target.mapping.isDefined()
                and s.getterExpression.isDefined() and
                    s.getterExpression.trim().length() > 0

        t.target = s.target.mapping.target.getPSMEquivalent();
        t.cardinality = s.equivalent("CreateCardinalityForTransferObjectRelationBinding");
        t.getterExpression = s.equivalent("CreateGetterExpressionForTransferObjectRelationBinding");
        t.name = "_" + s.name + "_" + esmUtils.getNamespaceElementFQName(s.eContainer).replace("::","_");
        s.eContainer.mapping.target.getPSMEquivalent().navigationProperties.add(t);
        
        log.debug("Created Navigation Property for transfer object relation binding: " + t.name);
}

@lazy
@greedy
rule CreateStaticNavigationForTransferObjectRelationDefaultValue 
    transform s : ESM!RelationFeature
    to t : JUDOPSM!StaticNavigation
    extends CreateNamedElement {
        guard:  s.isDefaultExpressionSupported()
                
            t.target = s.target.mapping.target.getPSMEquivalent();
            t.cardinality = s.equivalent("CreateCardinalityForTransferObjectRelationDefault");
            t.getterExpression = s.equivalent("CreateGetterExpressionForTransferObjectRelationDefault");
            t.name = "_" + s.name + "_default_" + s.eContainer.name;
            (s.eContainer).eContainer.getPSMEquivalent().elements.add(t);
            
            log.debug("Created Static Navigation for transfer object relation default: " + t.name);
}

@abstract
rule CreateNavigationPropertyForTransferObjectRelationRange
    transform s : ESM!RelationFeature
    to t : JUDOPSM!NavigationProperty
    extends CreateNamedElement {
        t.target = s.target.mapping.target.getPSMEquivalent();
        t.cardinality = s.equivalent("CreateCardinalityForTransferObjectRelationRange");
        t.name = "_" + s.name + "_range_" + esmUtils.getNamespaceElementFQName(s.eContainer).replace("::","_");
        s.eContainer.mapping.target.getPSMEquivalent().navigationProperties.add(t);
}

@lazy
@greedy
rule CreateNavigationPropertyForTransferObjectRelationDerivedRange
    transform s : ESM!RelationFeature
    to t : JUDOPSM!NavigationProperty
    extends CreateNavigationPropertyForTransferObjectRelationRange {
        guard: s.isDerivedRangeExpressionSupported()
            
            t.getterExpression = s.equivalent("CreateGetterExpressionForTransferObjectRelationDerivedRange");
            log.debug("Created Navigation Property for transfer object relation range: " + t.name);
}

@lazy
@greedy
rule CreateNavigationPropertyForTransferObjectRelationAnyRange
    transform s : ESM!RelationFeature
    to t : JUDOPSM!NavigationProperty
    extends CreateNavigationPropertyForTransferObjectRelationRange {
        guard: s.isAnyRangeExpressionSupported()

            t.getterExpression = s.equivalent("CreateGetterExpressionForTransferObjectRelationAnyRange");
            log.debug("Created Navigation Property for transfer object relation range: " + t.name);
}

@lazy
@greedy
rule CreateCardinalityForExposedGraphSelector
    transform s: ESM!ReferenceTypedElement
    to t: JUDOPSM!Cardinality {
      t.upper = s.upper;
      t.lower = s.lower;
}

rule CreateStaticNavigationForExposedGraph
	transform s : ESM!OneWayRelationMember
	to t : JUDOPSM!StaticNavigation
	extends CreateNamedElement {
	  guard: not s.eContainer.isMapped() and s.target.mapping.isDefined() and s.getterExpression.isDefined() and s.getterExpression.trim() <> ''

	  var ecoreUtil = new Native("org.eclipse.emf.ecore.util.EcoreUtil");
	  ecoreUtil.getRootContainer(s).equivalent("CreateGeneratedNavigationRootPackage").elements.add(t);
	  t.name = "_" + s.name + "_selector_" + s.eContainer.name;
	  t.cardinality = s.equivalent("CreateCardinalityForExposedGraphSelector");
	  t.getterExpression = s.equivalent("CreateGetterExpressionForExposedGraphSelector");
	  t.target = s.target.mapping.target.equivalent("CreateEntityType");

	  log.debug("Created StaticNavigation for ExposedGraph: " + t.name);
}

rule CreateNavigationPropertyForAccessTransferObjectRelationWithBinding
    transform s : ESM!Access
    to t : JUDOPSM!NavigationProperty
    extends CreateNamedElement {
        guard: s.eContainer.principal.isDefined() and s.eContainer.principal.isMapped()
            and s.accessType == ESM!AccessType#DERIVED

        t.target = s.target.mapping.target.getPSMEquivalent();
        t.cardinality = s.equivalent("CreateCardinalityForTransferObjectRelationBinding");
        t.getterExpression = s.equivalent("CreateGetterExpressionForAccessTransferObjectRelationWithBinding");
        t.name = "_" + s.name + "_" + esmUtils.getNamespaceElementFQName(s.eContainer).replace("::","_");
        s.eContainer.principal.mapping.target.getPSMEquivalent().navigationProperties.add(t);

        log.debug("Created Navigation Property for access transfer object relation binding: " + t.name);
}

rule CreateStaticNavigationForAccessTransferObjectRelationWithBinding
    transform s : ESM!Access
    to t : JUDOPSM!StaticNavigation
    extends CreateNamedElement {
        guard: (not s.eContainer.principal.isDefined() or
            (s.eContainer.principal.isDefined() and not s.eContainer.principal.isMapped()))
            and s.accessType == ESM!AccessType#DERIVED

        var ecoreUtil = new Native("org.eclipse.emf.ecore.util.EcoreUtil");
        ecoreUtil.getRootContainer(s).equivalent("CreateGeneratedNavigationRootPackage").elements.add(t);
        t.name = "_" + s.name + "_selector_" + s.eContainer.name;
        t.target = s.target.mapping.target.getPSMEquivalent();
        t.cardinality = s.equivalent("CreateCardinalityForTransferObjectRelationBinding");
        t.getterExpression = s.equivalent("CreateGetterExpressionForAccessTransferObjectRelationWithBinding");

        log.debug("Created StaticNavigation for access transfer object relation: " + t.name);
}
