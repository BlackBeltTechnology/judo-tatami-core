import "../namespace/namespace.etl";
import "../../../../operations/_importAll.eol";

@lazy
@greedy
rule CreateCardinalityForReferenceTypedElement
    transform s: ESM!ReferenceTypedElement
    to t: JUDOPSM!Cardinality {
      t.upper = s.upper;
      t.lower = s.lower;
}

rule CreateContainment
	transform s: ESM!OneWayRelationMember
	to t: JUDOPSM!Containment
	extends CreateNamedElement {
	  guard: s.memberType == ESM!MemberType#STORED
           and s.isComposition()
	  
	  t.cardinality = s.equivalent("CreateCardinalityForReferenceTypedElement");
	  s.eContainer.getPSMEquivalent().getRelations().add(t);
	  t.target = s.target.getPSMEquivalent();
	  
	  log.debug("Created Containment: " + t.name);
}

rule CreateAssociationEndWithoutPartner
	transform s: ESM!OneWayRelationMember
	to t: JUDOPSM!AssociationEnd
	extends CreateNamedElement {
	  guard: s.memberType == ESM!MemberType#STORED
           and not s.isComposition()
	  
	  t.cardinality = s.equivalent("CreateCardinalityForReferenceTypedElement");
	  t.reverseCascadeDelete = s.reverseCascadeDelete;
	  s.eContainer.getPSMEquivalent().relations.add(t);
	  t.target = s.target.getPSMEquivalent();
	  
	  log.debug("Created AssociationEnd: " + t.name);
}

rule CreateAssociationEndWithPartner
	transform s: ESM!TwoWayRelationMember
	to t: JUDOPSM!AssociationEnd
	extends CreateNamedElement {
		
	  t.cardinality = s.equivalent("CreateCardinalityForReferenceTypedElement");
	  t.reverseCascadeDelete = s.reverseCascadeDelete;
	  t.partner = s.partner.getPSMEquivalent();
	  s.eContainer.getPSMEquivalent().relations.add(t);
	  t.target = s.target.getPSMEquivalent();
	  
	  log.debug("Created AssociationEnd: " + t.name);
}

//optional references
@lazy
@greedy
rule CreateCardinalityForOptionalReferenceTypedElement
    transform s: ESM!ReferenceTypedElement
    to t: JUDOPSM!Cardinality {
      t.upper = s.upper;
      t.lower = 0;
}

rule CreateOptionalContainment
	transform s: ESM!OneWayRelationMember
	to t: JUDOPSM!Containment {
	  guard: s.memberType == ESM!MemberType#STORED
           and s.isComposition()
	  
	  t.name = "_Optional" + s.name.firstToUpperCase();
	  
	  t.cardinality = s.equivalent("CreateCardinalityForOptionalReferenceTypedElement");
	  s.eContainer.equivalent("CreateOptionalEntityType").getRelations().add(t);
	  t.target = s.target.equivalent("CreateOptionalEntityType");
	  
	  log.debug("Created Optional Containment: " + t.name);
}

rule CreateOptionalAssociationEndWithPartner
	transform s: ESM!TwoWayRelationMember
	to t: JUDOPSM!AssociationEnd {
		guard: s.isAggregation() or s.partner.isAggregation()
	
	  t.name = "_Optional" + s.name.firstToUpperCase();
	  
	  t.cardinality = s.equivalent("CreateCardinalityForOptionalReferenceTypedElement");
	  t.reverseCascadeDelete = s.reverseCascadeDelete;
	  t.partner = s.partner.equivalent("CreateOptionalAssociationEndWithPartner");
	  s.eContainer.equivalent("CreateOptionalEntityType").relations.add(t);
	  t.target = s.target.equivalent("CreateOptionalEntityType");
	  
	  log.debug("Created Optional AssociationEnd: " + t.name);
}
