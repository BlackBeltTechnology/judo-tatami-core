import "../../../../operations/_importAll.eol";

rule NavigationController
    transform s: ESM!esm::accesspoint::ActorType
    to t: UI!ui::NavigationController {
        guard: s.kind = ESM!esm::accesspoint::ActorKind#HUMAN 

        log.debug("NavigationController: " + s.name);
        t.name = s.fqName().anchor("NavigationController");
}

rule AccessMenuNavigationItem
    transform s: ESM!esm::ui::MenuItemAccess
    to t : UI!ui::NavigationItem {
		guard: not s.hidden and s.access.target.isTableDefined()

        log.debug("Create Access Navigation: " + s.access.fqName() );
        var application = s.access.eContainer.equivalent("Application");

        t.name = s.access.fqName().anchor("NavigationItem");
        t.label = s.getUiLabel();
        var icon = new UI!ui::Icon();
        icon.name = s.iconName;
        if (icon.name.isUndefined()) {
            icon.name = "texture";
        }
        t.icon = icon;

        t.target = s.access.mapTablePage(application, s.access.target);

        s.eContainer.equivalent("NavigationController").items.add(t);
}
