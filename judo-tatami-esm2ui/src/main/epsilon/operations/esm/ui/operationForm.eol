operation ESM!esm::ui::OperationForm createButtonFromOperationForm(application : UI!ui::Application) : UI!ui::Button {
	var t : UI!ui::Button = new UI!ui::Button();
	
	t.name = self.name;
    t.label = self.getUiLabel();
    t.col = self.col.asReal();
	t.row = self.row.asReal();
    
	if (self.stretch == ESM!esm::ui::Stretch#NONE) {
    	t.stretch = UI!ui::Stretch#NONE;
    } else if (self.stretch == ESM!esm::ui::Stretch#HORIZONTAL) {
        t.stretch = UI!ui::Stretch#HORIZONTAL;
    } else if (self.stretch == ESM!esm::ui::Stretch#VERTICAL) {
        t.stretch = UI!ui::Stretch#VERTICAL;
    } else {
        t.stretch = UI!ui::Stretch#BOTH;
    }
    
    if (self.fit == ESM!esm::ui::Fit#LOOSE) {
		t.fit = UI!ui::Fit#LOOSE;
	} else if (self.action == ESM!esm::ui::Fit#TIGHT) {
		t.fit = UI!ui::Fit#TIGHT;
	}
	
	t.sourceId = self.getID();

	log.debug("Create operation form: " + self.name);
	var op : ESM!esm::`operation`::Operation = self.getOperationReference.getMember();
	t.dataElement = op.mapOperationType(application, self.getRoot().eContainer.mapClassType(application));
	var callAction = new UI!ui::CallOperationAction();
	callAction.`operation` = op.mapOperationType(application, self.getRoot().eContainer.mapClassType(application));

	// Deal the mapping later, to avoid endless loop
	callAction.~originalOperationForm = self;
	callAction.~application = application;
	
	t.action = callAction;
    return t;
}

operation ESM!esm::ui::OperationForm map(application : UI!ui::Application) : UI!ui::Button {
    return self.createButtonFromOperationForm(application);
}
