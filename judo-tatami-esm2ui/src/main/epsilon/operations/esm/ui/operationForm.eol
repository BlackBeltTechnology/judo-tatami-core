operation ESM!esm::ui::OperationForm createButtonFromOperationForm(application : UI!ui::Application, page : UI!ui::PageDefinition) : UI!ui::Button {
    var t : UI!ui::Button = new UI!ui::Button();
    
    t.setVisualProperties(self, application);
    t.sourceId = self.getID();
    
    log.debug("Create operation form: " + self.name);
    var op : ESM!esm::`operation`::Operation = self.getOperationReference().getMember();
    t.dataElement = op.mapOperationType(application, self.getRoot().eContainer.mapClassType(application));
    var callAction = new UI!ui::CallOperationAction();
    callAction.`operation` = op.mapOperationType(application, self.getRoot().eContainer.mapClassType(application));
    
    if (self.iconName.isDefined()) {
        callAction.icon = new UI!ui::Icon();
        callAction.icon.name = self.iconName;
    }
    
    // Deal the mapping later, to avoid endless loop
    callAction.~originalOperationForm = self;
    callAction.~application = application;
    
    t.action = callAction;
    
    t.disabled = (page.pageType == UI!ui::PageType#UPDATE or page.pageType == UI!ui::PageType#OPERATION_INPUT or 
            (page.pageType == UI!ui::PageType#OPERATION_OUTPUT and op.isStateful));
    
    return t;
}

operation ESM!esm::ui::OperationForm map(application : UI!ui::Application, page : UI!ui::PageDefinition) : UI!ui::Button {
    return self.createButtonFromOperationForm(application, page);
}
