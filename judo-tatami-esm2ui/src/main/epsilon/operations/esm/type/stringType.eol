operation ESM!esm::type::StringType input(dataField : ESM!esm::ui::DataField, application : UI!ui::Application): UI!ui::Input {
    
    if (dataField.textMultiLine) {
        return self.fillInput(new UI!ui::TextArea(), dataField.row.asReal(), dataField.col.asReal(), dataField.iconName);
    } else {
        return self.fillInput(new UI!ui::TextInput(), dataField.row.asReal(), dataField.col.asReal(), dataField.iconName);
    }
}

operation ESM!esm::type::StringType formatted(): UI!ui::Formatted {
    return self.fillFormatted(new UI!ui::Formatted(), "%s", 1d, 2d);
}

operation ESM!esm::type::StringType formatted(dataField : ESM!esm::ui::DataField): UI!ui::Formatted {
    return self.fillFormatted(new UI!ui::Formatted(), "%s", dataField.row.asReal(), dataField.col.asReal());
}

@cached
operation ESM!esm::type::StringType fqName() {
    if (self.eContainer.isKindOf(ESM!esm::namespace::NamespaceElement)) {
        return esmUtils.getNamespaceElementFQName(self.eContainer) + "." + self.name;
    } else {
        return self.eContainer.name + "." + self.name;
    }
}

operation ESM!esm::type::StringType createStringType(application : UI!ui::Application) : UI!ui::data::StringType {
    var type = new UI!ui::data::StringType();
    type.name = self.fqName();
    type.regExp = self.regExp;
    type.maxLength = self.maxLength;
    type.operator = self.mapTypeOperationEnumeration(application);
    application.dataTypes.add(type);
    return type;
}

operation ESM!esm::type::StringType mapDataType(application : UI!ui::Application) : UI!ui::data::StringType {
    var type = application.dataTypes.selectOne(t | t.name = self.fqName());
    if (type.isUndefined()) {
        type = self.createStringType(application);
    }
    return type;
}

operation ESM!esm::type::StringType mapTypeOperationEnumeration(application : UI!ui::Application) : UI!ui::data::EnumerationType {
    var type = application.dataTypes.selectOne(t | t.name = "_StringOperation");
    if (type.isUndefined()) {
        type = createStringOperationEnumeration(application);
    }
    return type;
}
