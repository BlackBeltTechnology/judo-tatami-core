operation ESM!esm::type::TimestampType input(dataField : ESM!esm::ui::DataField, application : UI!ui::Application): UI!ui::Input {
    return self.fillInput(new UI!ui::DateTimeInput(), dataField.row.asReal(), dataField.col.asReal(), dataField.iconName);
}

operation ESM!esm::type::TimestampType formatted(): UI!ui::Formatted {
    return self.fillFormatted(new UI!ui::Formatted(), "%s", 1d, 2d);
}

@cached
operation ESM!esm::type::TimestampType fqName() {
    if (self.eContainer.isKindOf(ESM!esm::namespace::NamespaceElement)) {
        return esmUtils.getNamespaceElementFQName(self.eContainer) + "." + self.name;
    } else {
        return self.eContainer.name + "." + self.name;
    }
}

operation ESM!esm::type::TimestampType createTimestampType(application : UI!ui::Application) : UI!ui::data::TimestampType {
    var type = new UI!ui::data::TimestampType();
    type.name = self.fqName();
    type.operator = self.mapTypeOperationEnumeration(application);
    application.dataTypes.add(type);
    return type;
}

operation ESM!esm::type::TimestampType mapDataType(application : UI!ui::Application) : UI!ui::data::TimestampType {
    var type = application.dataTypes.selectOne(t | t.name = self.fqName());
    if (type.isUndefined()) {
        type = self.createTimestampType(application);
    }
    return type;
}

operation ESM!esm::type::TimestampType mapTypeOperationEnumeration(application : UI!ui::Application) : UI!ui::data::EnumerationType {
    var type = application.dataTypes.selectOne(t | t.name = "_NumericOperation");
    if (type.isUndefined()) {
        type = createNumericOperationEnumeration(application);
    }
    return type;
}
