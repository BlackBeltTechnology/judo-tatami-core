operation String anchor(anchor1 : String, anchor2 : String) : String {
	if (anchor1.isUndefined()) {
		return self;
	} else {
		if (anchor2.isUndefined()) {
			return self + "#" + anchor1;
		} else {
			return self.anchor(anchor1, null) + "/" + anchor2;
		}
	}
} 

operation String anchor(anchor1 : String) : String {
	return self.anchor(anchor1, null);
}

operation createBlankApplicationForPreview() : UI!ui::Application {
	
	var application : UI!ui::Application = new UI!ui::Application();
	
	var defaultLayout = new UI!ui::LayoutType();
    defaultLayout.cols = applicationColumns;
    defaultLayout.name = "default";
    defaultLayout.original = true;
    
    var dashboardForPreview = new UI!ui::PageDefinition();
    dashboardForPreview.name = previewElement.getName().anchor("preview","dashboard");
    
    var pageContainerForPreview : UI!ui::PageContainer = new UI!ui::PageContainer();
    pageContainerForPreview.name = previewElement.getName().anchor("preview","pageContainer");
    pageContainerForPreview.layoutType = defaultLayout;
    
    dashboardForPreview.containers.add(pageContainerForPreview);
    
    application.layoutTypes.add(defaultLayout);
    application.name = previewElement.getName().anchor("preview","application");
    application.navigationController = new UI!ui::NavigationController();
    application.navigationController.name = previewElement.getName().anchor("preview","navigationController");
    application.dashboard = dashboardForPreview;
    application.pages.add(dashboardForPreview);
    
    application.actor = new UI!ui::data::ClassType();
    application.actor.name = previewElement.getName().anchor("preview","classType");
    
    return application;
}

operation clonePagesOfInheritedRelations(application : UI!ui::Application) {

	var classTypes = application.dataElements.select(d | d.isKindOf(UI!ui::data::ClassType));
	
	for (classType : UI!ui::data::ClassType in classTypes) {
		for (relation : UI!ui::data::RelationType in classType.relations) {
			if (relation.~originalRelation.isDefined() and relation.~originalRelation.eContainer() <> classType.~originalClass) {
	
				var viewPage = application.pages.selectOne(p | p.~originalRelation == relation.~originalRelation and p.pageType == UI!ui::PageType#VIEW);
				if (viewPage.isDefined()) {
					var clone = ecoreUtil.copy(viewPage);
					clone.name = (esmUtils.getNamespaceElementFQName(classType.~originalClass) + "." + relation.~originalRelation.name).anchor("View");
					clone.dataElement = relation;
					clone.~original = viewPage;
					application.pages.add(clone);
				}
				
				var createPage = application.pages.selectOne(p | p.~originalRelation == relation.~originalRelation and p.pageType == UI!ui::PageType#CREATE);
				if (createPage.isDefined()) {
					var clone = ecoreUtil.copy(createPage);
					clone.name = (esmUtils.getNamespaceElementFQName(classType.~originalClass) + "." + relation.~originalRelation.name).anchor("Create");
					clone.dataElement = relation;
					clone.~original = createPage;
					application.pages.add(clone);
				}
				
				var updatePage = application.pages.selectOne(p | p.~originalRelation == relation.~originalRelation and p.pageType == UI!ui::PageType#UPDATE);
				if (updatePage.isDefined()) {
					var clone = ecoreUtil.copy(updatePage);
					clone.name = (esmUtils.getNamespaceElementFQName(classType.~originalClass) + "." + relation.~originalRelation.name).anchor("Edit");
					clone.dataElement = relation;
					clone.~original = updatePage;
					application.pages.add(clone);
				}
				
				var tablePage = application.pages.selectOne(p | p.~originalRelation == relation.~originalRelation and p.pageType == UI!ui::PageType#TABLE);
				if (tablePage.isDefined()) {
					var clone = ecoreUtil.copy(tablePage);
					clone.name = (esmUtils.getNamespaceElementFQName(classType.~originalClass) + "." + relation.~originalRelation.name).anchor("Table");
					clone.dataElement = relation;
					clone.~original = tablePage;
					application.pages.add(clone);
				}
			}
		}
	}
}

operation UI!ui::PageDefinition updateRelationPageContent() {
	log.debug("Updating page definition: " + self.name);
	
	for (container : UI!ui::PageContainer in self.containers) {
		container.update(self);
	}
}

operation UI!ui::VisualElement update(pageDef : UI!ui::PageDefinition) {
	
	log.debug("Updating visual element: " + self.name);
	
	var relation : UI!ui::data::RelationType = pageDef.dataElement;
	
	if (self.isKindOf(UI!ui::Container)) {
		for (child in self.children) {
			child.update(pageDef);
		}
	} else if (self.isKindOf(UI!ui::TabController)) {
		for (tab in self.tabs) {
			tab.element.update(pageDef);
		}
	} else if (self.isKindOf(UI!ui::Table)) {
		
		var old = self.dataElement; 
		
		if (pageDef.pageType == UI!ui::PageType#TABLE) {
			if (relation <> self.dataElement) {
				self.dataElement = relation;
				log.debug("Changed table page's data element from " + old.eContainer.name + "." + old.name + " to " + self.dataElement.eContainer.name + "." + self.dataElement.name );
			}
		}
		for (col in self.columns) {
			col.update(pageDef);
		}
	} else if (self.isKindOf(UI!ui::Button)) {
		if (self.action.isKindOf(UI!ui::PageAction)) {
			if (self.dataElement <> self.action.target.dataElement) {
				var application = self.action.target.~application;
				var clonedPage = application.pages.selectOne(p | p.~original.isDefined() and p.~original == self.action.target);
				if (clonedPage.isDefined()) {
					self.action.target = clonedPage;
				}	
			}
		}
	}
}
