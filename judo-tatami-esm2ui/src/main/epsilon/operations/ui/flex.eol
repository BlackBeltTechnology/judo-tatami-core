operation UI!ui::Flex wrapAndLabelFlex() : UI!ui::Flex {
    var wrapper = createVerticalFlex(self.name.anchor("LabelWrapper"), self.col, self.row + 1d);
    wrapper.crossAxisAlignment = UI!ui::CrossAxisAlignment#START;
    wrapper.mainAxisAlignment = UI!ui::MainAxisAlignment#START;
    if (self.isCard()) {
        wrapper.frame = self.frame;
    }
    
    wrapper.stretch = self.stretch;
    self.stretch = UI!ui::Stretch#BOTH;
    
    wrapper.~wrapper = true;
    
    var label : UI!ui::Label = new UI!ui::Label();
    label.row = 1d;
    label.col = self.col;
    label.stretch = UI!ui::Stretch#HORIZONTAL;
    label.name = self.name.anchor("Label");
    
    if (self.getIsLabelVisible()) {
        label.label = self.label;
        label.showLabel = true;
        label.labelMargin = new UI!ui::Margin();
    }
    
    if (self.getIsIconVisible()) {
        var icon : UI!ui::Icon = new UI!ui::Icon();
        icon.name = self.icon.name;
        label.icon = icon;
        label.showIcon = true;
        label.iconMargin = new UI!ui::Margin();
    }
    
    wrapper.children.add(label);
    wrapper.children.add(self);
    return wrapper;
}

