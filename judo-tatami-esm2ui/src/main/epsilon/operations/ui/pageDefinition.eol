operation UI!ui::PageDefinition calculateActions(application : UI!ui::Application) {
	var relationType = self.getRelationType();
	var classType = self.getTargetClassType();

	if (self.pageType == UI!ui::PageType#CREATE) {
		self.instanceActions.add(new UI!ui::BackAction());
		self.instanceActions.add(new UI!ui::SaveAction());
	} else if (self.pageType == UI!ui::PageType#UPDATE) {
		self.instanceActions.add(new UI!ui::BackAction());
		self.instanceActions.add(new UI!ui::SaveAction());
	} else if (self.pageType == UI!ui::PageType#VIEW) {
		self.instanceActions.add(new UI!ui::BackAction());

		if (classType.getIsClassBehaviourTypeDeletable()) {
			self.instanceActions.add(new UI!ui::DeleteAction());	
		}
		if (classType.getIsClassBehaviourTypeUpdatable()) {
		
			var page = application.pages.selectOne(p | p.pageType = UI!ui::PageType#UPDATE 
				and p.getRelationType() == relationType);
			if (page.isDefined()) {
				log.debug(" ========= PAGE DEF: UPDATE: " + relationType.eContainer.name + "." + relationType.name + " - " + page.name);
			    var editAction = new UI!ui::EditAction();
				editAction.target = page;
				self.instanceActions.add(editAction);
			} else {
				log.warn("No update page defined for: " + self.name + " - " + relationType.eContainer.name + "." + relationType.name);
			}
		}
	} else if (self.pageType == UI!ui::PageType#TABLE) {	
		if (relationType.isUndefined()) {
			throw "Table type have to be mapped to relation type";
		}

	    if (not relationType.isAccess) {
			self.instanceActions.add(new UI!ui::BackAction());
		}

		if (relationType.getIsRelationBehaviourTypeCreatable()) {
			var page = application.pages.selectOne(p | p.pageType = UI!ui::PageType#CREATE 
				and p.getRelationType() == relationType);
			if (page.isDefined()) {
				log.debug(" ========= PAGE DEF: CREATE: " + relationType.eContainer.name + "." + relationType.name + " - " + page.name);
			    var createAction = new UI!ui::CreateAction();
				createAction.target = page;
				self.instanceActions.add(createAction);	
			} else {
				log.warn("No create page defined for: " + self.name + " - " + relationType.eContainer.name + "." + relationType.name);
			}
		}
		
		if (relationType.getIsRelationBehaviourTypeSetable()) {
			self.instanceActions.add(new UI!ui::SetAction());
		}
		if (relationType.getIsRelationBehaviourTypeUnsetable()) {
			self.instanceActions.add(new UI!ui::UnsetAction());
		}
		if (relationType.getIsRelationBehaviourTypeAddable()) {
			self.instanceActions.add(new UI!ui::AddAction());
		}
	}
}