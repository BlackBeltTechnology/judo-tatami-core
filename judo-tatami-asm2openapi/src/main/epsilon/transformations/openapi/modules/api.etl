import "../../../operations/openapi/_importAll.eol";

rule CreateOpenAPIAccessPoint
    transform s : ASM!EClass
    to t : OPENAPI!API {
        guard: asmUtils.isAccessPoint(s)
        
        t.swagger = "2.0";

        t.host = "localhost:8181"; // TODO
        t.basePath = "/cxf/" + asmUtils.getClassifierFQName(s).toOpenAPIPath();
        
        t.schemes.add(OPENAPI!SchemeType#http);
        t.schemes.add(OPENAPI!SchemeType#https);
        t.produces.add("application/json");
}

rule CreateOpenAPIAccessPointInfo
    transform s : ASM!EClass
    to t : OPENAPI!Info {
        guard: asmUtils.isAccessPoint(s)
        
        t.version = "1.0.0"; // TODO
        t.title = s.name;
        
        s.equivalent("CreateOpenAPIAccessPoint").info = t;
}

rule CreateInternalServerErrorDeclaration
    transform s : ASM!EClass
    to t : OPENAPI!ResponseDeclaration {
        guard: asmUtils.isAccessPoint(s)

        t.name = "InternalServerError";
        
        s.equivalent("CreateOpenAPIAccessPoint").responses.add(t);
}

rule CreateInternalServerErrorDefinition
    transform s : ASM!EClass
    to t : OPENAPI!ResponseDefinition {
        guard: asmUtils.isAccessPoint(s)

        t.description = "Internal server error";
        
        s.equivalent("CreateInternalServerErrorDeclaration").response = t;
}
