import "../../../operations/openapi/_importAll.eol";

rule CreateOpenAPIActorType
    transform s : ASM!EClass
    to t : OPENAPI!API {
        guard: asmUtils.isActorType(s)
        
        t.swagger = "2.0";

        t.host = "localhost:8181"; // TODO
        t.basePath = "/api/" + asmUtils.getClassifierFQName(s).replace("[#\\.]", "/").replace("[^0-9a-zA-Z_~/]", "_");
        
        t.schemes.add(OPENAPI!SchemeType#http);
        t.schemes.add(OPENAPI!SchemeType#https);
        t.produces.add("application/json");
}

rule CreateOpenAPIActorTypeInfo
    transform s : ASM!EClass
    to t : OPENAPI!Info {
        guard: asmUtils.isActorType(s)
        
        t.version = "1.0.0"; // TODO
        t.title = s.name;
        
        s.equivalent("CreateOpenAPIActorType").info = t;
}

rule CreateInternalServerErrorDeclaration
    transform s : ASM!EClass
    to t : OPENAPI!ResponseDeclaration {
        guard: asmUtils.isActorType(s)

        t.name = "InternalServerError";
        
        s.equivalent("CreateOpenAPIActorType").responses.add(t);
}

rule CreateInternalServerErrorDefinition
    transform s : ASM!EClass
    to t : OPENAPI!ResponseDefinition {
        guard: asmUtils.isActorType(s)

        t.description = "Internal server error";
        
        s.equivalent("CreateInternalServerErrorDeclaration").response = t;
}
