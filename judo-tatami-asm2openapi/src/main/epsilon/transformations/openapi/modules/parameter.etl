import "../../../operations/asm/_importAll.eol";
import "../../../operations/asm/accesspoint/_importAll.eol";
import "../../../operations/openapi/_importAll.eol";

import "api.etl";
import "schema.etl";

rule CreateEntityIdParameterDeclaration
    transform s : ASM!EStringToStringMapEntry
    to t : OPENAPI!ParameterDeclaration {
        guard: s.key.startsWith(".accessPoints.") and s.getAnnotatedClass().isDefined() and asmUtils.isEntityType(s.getAnnotatedClass())
        
        var accessPoint = asmUtils.resolve(s.value).orElse(null);
        
        t.name = "__identifier__" + asmUtils.getClassifierFQName(s.getAnnotatedClass()).toOpenAPIName();
        
        accessPoint.equivalent("CreateOpenAPIAccessPoint").parameters.add(t);
}

rule CreateEntityIdParameterDefinition
    transform s : ASM!EStringToStringMapEntry
    to t : OPENAPI!ParameterDefinition {
        guard: s.key.startsWith(".accessPoints.") and s.getAnnotatedClass().isDefined() and asmUtils.isEntityType(s.getAnnotatedClass())
        
        t.name = "__identifier";
        t.`in` = OPENAPI!ParameterLocation#header;
        t.type = OPENAPI!JSONDataType#string;
        t.description = "ID of " + s.getAnnotatedClass().name + " instance";
        t.required = true;
        
        s.equivalent("CreateEntityIdParameterDeclaration").parameter = t;
}

rule CreateParameterDeclaration
    transform s : ASM!EStringToStringMapEntry
    to t : OPENAPI!ParameterDeclaration {
        guard: s.key.startsWith(".accessPoints.") and s.getAnnotatedParameter().isDefined()
        
        var accessPoint = asmUtils.resolve(s.value).orElse(null);
        
        t.name = (asmUtils.getOperationFQName(s.getAnnotatedParameter().eOperation) + "__" + s.getAnnotatedParameter().name).toOpenAPIName();
        
        accessPoint.equivalent("CreateOpenAPIAccessPoint").parameters.add(t);
}

rule CreateParameterDefinition
    transform s : ASM!EStringToStringMapEntry
    to t : OPENAPI!ParameterDefinition {
        guard: s.key.startsWith(".accessPoints.") and s.getAnnotatedParameter().isDefined()
        
        t.name = s.getAnnotatedParameter().name;
        t.required = true;
        t.`in` = OPENAPI!ParameterLocation#body;
        t.schema = new OPENAPI!Schema;
        t.schema.ref = new OPENAPI!SchemaReference();
        
        var annotation = asmUtils.getExtensionAnnotation(s.getAnnotatedParameter().eType, false);
        if (annotation.isPresent()) {
            var a = annotation.get().details.selectOne(d | d.key.startsWith(".accessPoints.") and d.value = s.value);

	        if (s.getAnnotatedParameter().many) {
	            t.schema.ref.ref = a.equivalent("CreateTransferObjectArraySchemaDeclaration").getReference();
	        } else {
	            t.schema.ref.ref = a.equivalent("CreateTransferObjectSingleSchemaDeclaration").getReference();
	        }
        } else {
            throw "Parameter is not prepared";
        }
        
        s.equivalent("CreateParameterDeclaration").parameter = t;
}
