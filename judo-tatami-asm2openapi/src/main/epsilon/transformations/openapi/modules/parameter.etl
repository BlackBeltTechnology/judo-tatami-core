import "api.etl";
import "schema.etl";

rule CreateEntityIdParameterDeclaration
    transform s : ASM!EAnnotation
    to t : OPENAPI!ParameterDeclaration {
        guard: s.eContainer.isKindOf(ASM!EClass) and s.source == asmUtils.getAnnotationUri("exposedBy") and asmUtils.isEntityType(s.eContainer)
        
        t.name = "__identifier__" + asmUtils.getClassifierFQName(s.eContainer).toOpenAPIName();
        
        var accessPoint = asmUtils.getResolvedExposedBy(s);
        if (accessPoint.isPresent()) {
            accessPoint.get().equivalent("CreateOpenAPIAccessPoint").parameters.add(t);
        } else {
            delete t;
        }
}

rule CreateEntityIdParameterDefinition
    transform s : ASM!EAnnotation
    to t : OPENAPI!ParameterDefinition {
        guard: s.eContainer.isKindOf(ASM!EClass) and s.source == asmUtils.getAnnotationUri("exposedBy") and asmUtils.isEntityType(s.eContainer)
        
        t.name = "__identifier";
        t.`in` = OPENAPI!ParameterLocation#header;
        t.type = OPENAPI!JSONDataType#string;
        t.description = "ID of " + s.eContainer.name + " instance";
        t.required = true;
        
        s.equivalent("CreateEntityIdParameterDeclaration").parameter = t;
}

rule CreateParameterDeclaration
    transform s : ASM!EAnnotation
    to t : OPENAPI!ParameterDeclaration {
        guard: s.eContainer.isKindOf(ASM!EParameter) and s.source == asmUtils.getAnnotationUri("exposedBy")
        
        t.name = (asmUtils.getOperationFQName(s.eContainer.eOperation) + "__" + s.eContainer.name).toOpenAPIName();
        
        var accessPoint = asmUtils.getResolvedExposedBy(s);
        if (accessPoint.isPresent()) {
            accessPoint.get().equivalent("CreateOpenAPIAccessPoint").parameters.add(t);
        } else {
            delete t;
        }
}

rule CreateParameterDefinition
    transform s : ASM!EAnnotation
    to t : OPENAPI!ParameterDefinition {
        guard: s.eContainer.isKindOf(ASM!EParameter) and s.source == asmUtils.getAnnotationUri("exposedBy")
        
        t.name = s.eContainer.name;
        t.required = s.eContainer.required;
        t.`in` = OPENAPI!ParameterLocation#body;
        
        s.equivalent("CreateParameterDeclaration").parameter = t;
}

rule CreateParameterDefinitionSchema
    transform s : ASM!EAnnotation
    to t : OPENAPI!Schema {
        guard: s.eContainer.isKindOf(ASM!EParameter) and s.source == asmUtils.getAnnotationUri("exposedBy")
        
        s.equivalent("CreateParameterDefinition").schema = t;
}

rule CreateParameterDefinitionSchemaReference
    transform s : ASM!EAnnotation
    to t : OPENAPI!SchemaReference {
        guard: s.eContainer.isKindOf(ASM!EParameter) and s.source == asmUtils.getAnnotationUri("exposedBy") and s.eContainer.eType.isDefined()

        if (s.eContainer.eType.isKindOf(ASM!EClass)) {
            var transferObjectTypeExposedByAnnotation = asmUtils.getExtensionAnnotationListByName(s.eContainer.eType, "exposedBy").selectOne(a | a.details.get("value") == s.details.get("value"));
		    if (transferObjectTypeExposedByAnnotation.isDefined()) {
		        var behaviour = asmUtils.getBehaviour(s.eContainer.eOperation);
		        var behaviourRelation = asmUtils.getRelationOfOperationWithDefaultBehaviour(s.eContainer.eOperation);

		        if (behaviour.present and behaviour.get().toString() == "CREATE") {
                    if (s.eContainer.many) {
                        t.ref = transferObjectTypeExposedByAnnotation.equivalent("CreateTransferObjectArraySchemaDeclarationExtended").getReference();
                    } else {
                        t.ref = transferObjectTypeExposedByAnnotation.equivalent("CreateTransferObjectSingleSchemaDeclarationExtended").getReference();
                    }
                } else if (behaviour.present and (behaviour.get().toString() == "DELETE" or behaviour.get().toString() == "UNSET")) {
                    if (s.eContainer.many) {
                        throw "Unsupported parameter";
                    } else {
                        t.ref = asmUtils.getAllAccessPoints().selectOne(ap | asmUtils.getClassifierFQName(ap) == s.details.get("value")).equivalent("CreateTransferObjectSingleSchemaDeclarationForDelete").getReference();
                    }
                } else if (behaviour.present and (behaviour.get().toString() == "SET" or behaviour.get().toString() == "ADD_ALL" or behaviour.get().toString() == "REMOVE_ALL") and behaviourRelation.present) {
                    if (s.eContainer.many) {
                        throw "Unsupported parameter";
                    } else {
                        t.ref = s.equivalent("CreateParameterDefinitionSchemaReferenceForReferenceChange").getReference();
                    }
		        } else {
                    if (s.eContainer.many) {
                        t.ref = transferObjectTypeExposedByAnnotation.equivalent("CreateTransferObjectArraySchemaDeclaration").getReference();
                    } else {
                        t.ref = transferObjectTypeExposedByAnnotation.equivalent("CreateTransferObjectSingleSchemaDeclaration").getReference();
                    }
		        }
		    }
        }
        
        s.equivalent("CreateParameterDefinitionSchema").ref = t;
}

@lazy
rule CreateParameterDefinitionSchemaReferenceForReferenceChange
    transform s : ASM!EAnnotation
    to t : OPENAPI!SchemaDeclaration {
        guard: s.eContainer.isKindOf(ASM!EParameter) and s.source == asmUtils.getAnnotationUri("exposedBy") and s.eContainer.eType.isDefined() and s.eContainer.eType.isKindOf(ASM!EClass)

        t.name = (asmUtils.getOperationFQName(s.eContainer.eOperation) + "__" + s.eContainer.name).toOpenAPIName();

        t.schema = new OPENAPI!Schema;
        t.schema.description = "Schema of change instance reference";
        t.schema.type = OPENAPI!JSONDataType#object;

        var id = new OPENAPI!PropertyDeclaration;
        id.name = "__identifier";

        id.property = new OPENAPI!Property;
        id.property.description = "ID of instance";
        id.property.type = OPENAPI!JSONDataType#string;

        t.schema.properties.add(id);

        var ref = new OPENAPI!PropertyDeclaration;

        var reference = asmUtils.getRelationOfOperationWithDefaultBehaviour(s.eContainer.eOperation).get();
        ref.name = reference.name;

        ref.property = new OPENAPI!Property;
        ref.property.description = "Referenced element(s)";

        var refId = new OPENAPI!PropertyDeclaration;
        refId.name = "__identifier";

        refId.property = new OPENAPI!Property;
        refId.property.description = "ID of referenced instance";
        refId.property.type = OPENAPI!JSONDataType#string;

        if (reference.many) {
            ref.property.type = OPENAPI!JSONDataType#array;
            ref.property.items = new OPENAPI!Property;
            ref.property.items.type = OPENAPI!JSONDataType#object;
            ref.property.items.properties.add(refId);
        } else {
            ref.property.type = OPENAPI!JSONDataType#object;
            ref.property.properties.add(refId);
        }

        t.schema.properties.add(ref);

        var ap = asmUtils.getAllAccessPoints().selectOne(ap | asmUtils.getClassifierFQName(ap) == s.details.get("value"));
        ap.equivalent("CreateOpenAPIAccessPoint").definitions.add(t);
}
