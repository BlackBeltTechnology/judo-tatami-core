import "../../../operations/asm/_importAll.eol";
import "../../../operations/asm/accesspoint/_importAll.eol";
import "../../../operations/openapi/_importAll.eol";

import "api.etl";

rule CreateTransferObjectSingleSchemaDeclaration
    transform s : ASM!EStringToStringMapEntry
    to t : OPENAPI!SchemaDeclaration {
        guard: s.key.startsWith(".accessPoints.") and s.getAnnotatedClass().isDefined() and not asmUtils.isEntityType(s.getAnnotatedClass())
        
        var accessPoint = asmUtils.resolve(s.value).orElse(null);
        
        t.name = asmUtils.getClassifierFQName(s.getAnnotatedClass()).toOpenAPIName();

        accessPoint.equivalent("CreateOpenAPIAccessPoint").definitions.add(t);
}

rule CreateTransferObjectSingleSchemaDefinition
    transform s : ASM!EStringToStringMapEntry
    to t : OPENAPI!Schema {
        guard: s.key.startsWith(".accessPoints.") and s.getAnnotatedClass().isDefined() and not asmUtils.isEntityType(s.getAnnotatedClass())
        
        t.description = "Schema of " + s.getAnnotatedClass().name;
        t.type = OPENAPI!JSONDataType#object;

        if (asmUtils.isMappedTransferObjectType(s.getAnnotatedClass())) {
            var id = new OPENAPI!PropertyDeclaration;
            id.name = "__identifier";

            id.property = new OPENAPI!Property;
            id.property.description = "ID of " + asmUtils.getEntityType(s.getAnnotatedClass()).get().name + " instance";
            id.property.type = OPENAPI!JSONDataType#string;

            t.properties.add(id);
        }
        
        s.equivalent("CreateTransferObjectSingleSchemaDeclaration").schema = t;
}

rule CreateTransferObjectArraySchemaDeclaration
    transform s : ASM!EStringToStringMapEntry
    to t : OPENAPI!SchemaDeclaration {
        guard: s.key.startsWith(".accessPoints.") and s.getAnnotatedClass().isDefined() and not asmUtils.isEntityType(s.getAnnotatedClass())
        
        var accessPoint = asmUtils.resolve(s.value).orElse(null);
        
        t.name = "arrayOf__" + asmUtils.getClassifierFQName(s.getAnnotatedClass()).toOpenAPIName();
        
        accessPoint.equivalent("CreateOpenAPIAccessPoint").definitions.add(t);
}

rule CreateTransferObjectArraySchemaDefinition
    transform s : ASM!EStringToStringMapEntry
    to t : OPENAPI!Schema {
        guard: s.key.startsWith(".accessPoints.") and s.getAnnotatedClass().isDefined() and not asmUtils.isEntityType(s.getAnnotatedClass())
        
        t.description = "Arrays of " + s.getAnnotatedClass().name + "s";
        t.type = OPENAPI!JSONDataType#array;
        
        s.equivalent("CreateTransferObjectArraySchemaDeclaration").schema = t;
}

rule CreateTransferObjectArrayItemsDefinition
    transform s : ASM!EStringToStringMapEntry
    to t : OPENAPI!Property {
        guard: s.key.startsWith(".accessPoints.") and s.getAnnotatedClass().isDefined() and not asmUtils.isEntityType(s.getAnnotatedClass())
        
        t.ref = new OPENAPI!SchemaReference;
        t.ref.ref = s.equivalent("CreateTransferObjectSingleSchemaDeclaration").getReference();
        
        s.equivalent("CreateTransferObjectArraySchemaDefinition").items = t;
}
