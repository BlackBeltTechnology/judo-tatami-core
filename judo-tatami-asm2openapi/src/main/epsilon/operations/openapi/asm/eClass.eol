operation ASM!EClass isContainedBy(container : ASM!EClass) : Boolean {
    return container.getContainedEClasses().includes(self);
}

@cached
operation ASM!EClass getContainedEClasses() : Set {
    var containments = self.eAllContainments.collect(c | c.eReferenceType).asSet();
    var collected = new Set();
    
    collected.addAll(containments);
    
    for (c in containments) {
        c.collectContainedEClasses(collected);
    }
    
    return collected;
}

operation ASM!EClass collectContainedEClasses(collected : Set) {
    var newContainments = new Set();
    for (c in self.eAllContainments) {
        if (not collected.includes(c.eReferenceType)) {
            newContainments.add(c.eReferenceType);    
        }
    } 
    collected.addAll(newContainments);
    for (c in newContainments) {
        c.collectContainedEClasses(collected);
    }
}